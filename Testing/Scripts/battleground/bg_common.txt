// (c) 2008 - 2012 eAmod Project; Andres Garbanzo / Zephyrus
//
//  - gaiaro.staff@yahoo.com
//  - MSN andresjgm.cr@hotmail.com
//  - Skype: Zephyrus_cr
//  - Site: http://dev.terra-gaming.com
//
// This file is NOT public - you are not allowed to distribute it.
// Authorized Server List : http://dev.terra-gaming.com/index.php?/topic/72-authorized-eamod-servers/
// eAmod is a non Free, extended version of eAthena Ragnarok Private Server.

// ==============================================================================
// BattleGround System - Common NPCs
// ==============================================================================

// MapFlags
// *********************************************************************

bat_room	mapflag	nomemo
bat_room	mapflag	nowarpto
bat_room	mapflag	nobranch
bat_room	mapflag	nopenalty
bat_room	mapflag	noteleport
bat_room	mapflag	nosave	SavePoint
bat_room	mapflag	allow_bg_items
// Mapflag Town is required to Join BG Queues
bat_room	mapflag	town

// Battleground Queue Admin
// *********************************************************************
-	script	BG_Queue_Join	-1,{
	end;

OnInit:
	// Configuration
	// *****************************************************
	// Battleground Rotation - Main NPC name
	setarray .BG_Arenas$[0],	     "Flavius_CTF",  "Tierra_EOS",     "Tierra_TI",     "Flavius_TD","Tierra_Boss","Conquest",   "Flavius_SC","Tierra_DOM",    "Rush";
	// Battleground Arena Name - for announcements
	setarray .BG_Names$[0],		"Capture the Flag","Eye of Storm","Triple Inferno","Team DeathMatch",    "Bossnia","Conquest","Stone Control","Domination",    "Rush";
	// Minimum Players to start the BG
	setarray .BG_Min[0],	                         5,             5,               5,                5,            5,        20,              5,           5,        15;
	// Maximum Players per Team
	setarray .BG_Max[0],	                        35,            35,              35,               35,           35,        50,             35,          35,        30;
	// BG Message Colors
	setarray .BG_Color$[0],	                "0xA0522D",    "0x4169E1",      "0x696969",       "0x808000",   "0x483D8B","0xFFA500",     "0x9ACD32",  "0x4169E1","0xDDA0DD";
	// Team Building Mode : 0 = Lineal | 1 = Random | 2 = Class Priority | 3 = Faction Mode | 4 = Team Color ( by script set Bat_Team,N; )
	set .TeamMode, 0;
	// Main Code
	// *****************************************************
	set .BG_Count, getarraysize(.BG_Arenas$);
	set .BG_Queue, bg_queue_create("Battleground Arena","BG_Queue_Join::OnJoinEvent",80);
	set .VS_Queue, bg_queue_create("Free For All Arena","BG_Queue_Join::OnVersusJoin",80);
	// Move to Next Arena
	if( $BG_Index >= .BG_Count ) set $BG_Index,0; // Restart Rotation
	set .BG_Arena$,.BG_Arenas$[$BG_Index];
	set .Ready, 1;
	donpcevent "BG_Queue_Join::OnDoHappyHour";
	initnpctimer; // Rotation if BG don't start... Comment if you don't want to use this method

	// Comment the next two lines if you don't want to allow @joinbg and @leavebg commands.
	bindatcmd "joinbg","BG_Queue_Join::OnDoJoin",0,99;
	bindatcmd "leavebg","BG_Queue_Join::OnDoLeave",0,99;
	end;

OnDoJoin: // @joinbg
	if( checkquest(8506,PLAYTIME) == 2 )
		erasequest 8506;

	if( checkquest(8506,PLAYTIME) != -1 )
	{
		dispbottom "[You are a Deserter. You can't participate until the indicator goes off]";
		end;
	}

	bg_queue_join .BG_Queue;
	end;

OnDoLeave: // @leavebg
	bg_queue_leave .BG_Queue;
	end;

OnRotate:
OnTimer120000: // Rotation if BG don't start... Comment if you don't want to use this method. 120 = 2 Minutes
	set $@BG_Status, 0;
	// Warps Teams
	bg_warp $@BG_Team1,"bat_room",155,150;
	bg_warp $@BG_Team2,"bat_room",155,150;
	bg_warp $@BG_Team3,"bat_room",155,150;
	// Destroy Teams
	bg_destroy $@BG_Team1; set $@BG_Team1, 0;
	bg_destroy $@BG_Team2; set $@BG_Team2, 0;
	bg_destroy $@BG_Team3; set $@BG_Team3, 0;

	// Move to Next Arena
	if( set($BG_Index,$BG_Index + 1) >= .BG_Count )
		set $BG_Index,0; // Restart Rotation

	set .BG_Arena$,.BG_Arenas$[$BG_Index];
	initnpctimer; // Rotation if BG don't start... Comment if you don't want to use this method

	// Trigger the Event - Maybe people is waiting...
OnJoinEvent:
	if( $@BG_Status == 2 )
		end; // BG Ending... Must wait until OnRotate is called

	set .@Queue_Count, bg_queue_data(.BG_Queue,0);
	switch( $@BG_Status )
	{
	case 0: // Idle - Ready to Start
		if( .BG_Arena$ == "Tierra_TI" )
			set .@Req, .BG_Min[$BG_Index] * 3;
		else
			set .@Req, .BG_Min[$BG_Index] * 2;

		if( bg_queue_checkstart(.BG_Queue,.TeamMode,( (.BG_Arena$ == "Tierra_TI") ? 3 : 2 ),.BG_Min[$BG_Index]) )
		{
			donpcevent .BG_Arena$ + "::OnBuildTeams";

			// Fill Teams with Queue Members			
			if( .BG_Arenas$[$BG_Index] == "Tierra_TI" )
				bg_queue2teams .BG_Queue,.BG_Min[$BG_Index],.BG_Max[$BG_Index],.TeamMode,$@BG_Team1,$@BG_Team2,$@BG_Team3;
			else
				bg_queue2teams .BG_Queue,.BG_Min[$BG_Index],.BG_Max[$BG_Index],.TeamMode,$@BG_Team1,$@BG_Team2;

			stopnpctimer; // Rotation if BG don't start... Comment if you don't want to use this method

			set $@BG_Status,1;
			set .@msg$, "Battleground -- " + .BG_Names$[$BG_Index] + " -- Begining";
			bgannounce .@msg$,.BG_Color$[$BG_Index];

			donpcevent .BG_Arena$ + "::OnReady";
		}
		else
		{
			set .@msg$, "Battleground -- " + .BG_Names$[$BG_Index] + " -- " + (.@Req - .@Queue_Count) + " more players to start.";
			announce .@msg$,0,.BG_Color$[$BG_Index];
		}
		break;
	case 1: // Running - Others can join
		if( .@Queue_Count > 0 )
		{
			if( .BG_Arena$ == "Tierra_TI" )
			{
				bg_balance_teams .BG_Queue,.BG_Max[$BG_Index],.TeamMode,$@BG_Team1,$@BG_Team2,$@BG_Team3;
				set .@BG_Count1, bg_get_data($@BG_Team1,0);
				set .@BG_Count2, bg_get_data($@BG_Team2,0);
				set .@BG_Count3, bg_get_data($@BG_Team3,0);
				set .@msg$, "Battleground -- " + .BG_Names$[$BG_Index] + " -- B: " + .@BG_Count1 + "/" + .BG_Max[$BG_Index] + ", R: " + .@BG_Count2 + "/" + .BG_Max[$BG_Index] + ", G: " + .@BG_Count3 + "/" + .BG_Max[$BG_Index] + " (Playing)";
			}
			else
			{
				bg_balance_teams .BG_Queue,.BG_Max[$BG_Index],.TeamMode,$@BG_Team1,$@BG_Team2;
				set .@BG_Count1, bg_get_data($@BG_Team1,0);
				set .@BG_Count2, bg_get_data($@BG_Team2,0);
				set .@msg$, "Battleground -- " + .BG_Names$[$BG_Index] + " -- B: " + .@BG_Count1 + "/" + .BG_Max[$BG_Index] + ", R: " + .@BG_Count2 + "/" + .BG_Max[$BG_Index] + " (Playing)";
			}

			bgannounce .@msg$,.BG_Color$[$BG_Index];
		}
		break;
	}
	end;

OnVersusJoin:
	if( $@VS_Status != 0 )
		end;

	set .@Queue_Count, bg_queue_data(.VS_Queue,0);
	if( .@Queue_Count >= 10 )
	{
		donpcevent "FFA_Arena::OnBuildTeams";
		bg_queue2teams .VS_Queue,1,1,0,$@VS_Team[0],$@VS_Team[1],$@VS_Team[2],$@VS_Team[3],$@VS_Team[4],$@VS_Team[5],$@VS_Team[6],$@VS_Team[7],$@VS_Team[8],$@VS_Team[9];
		set $@VS_Status,1;
		announce "Battleground -- Free For All -- Begining",0,0x483D8B;
		donpcevent "FFA_Arena::OnReady";
	}
	else
		announce "Battleground -- Free For All -- " + (10 - .@Queue_Count) + " more players to start",0,0x483D8B;
	end;

OnFri0600: // Friday, 6 a.m.
	rankreset 1; // Ranking Reset
	end;

// BattleGround Happy Hour
OnDoHappyHour:
OnClock1600:
OnClock1800:
OnClock2000:
OnClock2200:
	switch( gettime(4) )
	{
	case 1:
	case 3:
	case 5:
		if( gettime(3) >= 20 && gettime(3) < 22 )
		{
			announce "-- Battleground Happy Hour has begun | Ranked Arena Mode --",0,0x00FF00;
			setbattleflag "bg_reward_rates",120; // +20% Reward Rates
			setbattleflag "bg_ranked_mode",1;
			end;
		}
		
		if( gettime(3) == 22 )
		{
			announce "-- Battleground Happy Hour is over | Regular Arena Mode --",0,0x00BFFF;
			setbattleflag "bg_reward_rates",100; // Normal Rates
			setbattleflag "bg_ranked_mode",0;
			end;
		}
		break;
	case 2:
	case 4:
	case 6:
		if( gettime(3) >= 16 && gettime(3) < 18 )
		{
			announce "-- Battleground Happy Hour has begun | Ranked Arena Mode --",0,0x00FF00;
			setbattleflag "bg_reward_rates",120; // +20% Reward Rates
			setbattleflag "bg_ranked_mode",1;
			end;
		}
		
		if( gettime(3) == 18 )
		{
			announce "-- Battleground Happy Hour is over | Regular Arena Mode --",0,0x00BFFF;
			setbattleflag "bg_reward_rates",100; // Normal Rates
			setbattleflag "bg_ranked_mode",0;
			end;
		}
		break;
	}
	end;
}

// Registration NPC
// *********************************************************************
-	script	BG_Register	-1,{

	if( getvariableofnpc(.Ready,"BG_Queue_Join") == 0 )
		donpcevent "BG_Queue_Join::OnInit";

	mes "[^FFA500Battle Recruiter^000000]";
	mes "This is the Battleground Information and Registration service.";
	mes "What do you want to do?";
	next;
	switch( select("^FFA500Battleground Arenas^000000:^0000FFFree For All Arena^000000:Warp to Battle Room") )
	{
	case 1:
		mes "[^FFA500Battle Recruiter^000000]";
		mes "Battlegrounds Arena are different kinds of games where Teams fight for victory.";
		mes "What do you want to do?";
		next;
		switch( select("Register:Party Register:Leave Queue:Battleground Help") )
		{
		case 1:
			if( BaseLevel < 80 )
			{
				mes "[^FFA500Battle Recruiter^000000]";
				mes "Min required level to join a battleground is 80.";
				close;
			}
			if( checkquest(8506,PLAYTIME) == 2 )
				erasequest 8506;
			if( checkquest(8506,PLAYTIME) != -1 )
			{
				mes "[^FFA500Battle Recruiter^000000]";
				mes "You are a Deserter. You can't participate until the indicator goes off";
				close;
			}

			mes "[^FFA500Battle Recruiter^000000]";
			mes "Let's proceed with the registration...";
			mes "You must wait on any City until BG starts.";
			mes "Close this window to continue...";
			close2;
			bg_queue_join getvariableofnpc(.BG_Queue,"BG_Queue_Join");
			end;
		case 2:
			if( getcharid(1) == 0 )
			{
				mes "[^FFA500Battle Recruiter^000000]";
				mes "You are not on a Party.";
				close;
			}

			mes "[^FFA500Battle Recruiter^000000]";
			mes "Let's proceed with the registration...";
			mes "You must wait on any City until BG starts.";
			mes "Close this window to continue...";
			close2;
			bg_queue_partyjoin getcharid(1),getvariableofnpc(.BG_Queue,"BG_Queue_Join");
			end;
		case 3:
			mes "[^FFA500Battle Recruiter^000000]";
			mes "If you are registered, you will be removed.";
			mes "Are you sure?";
			next;
			if( select("Yes, leave queue:No, I will stay") == 2 )
			{
				mes "[^FFA500Battle Recruiter^000000]";
				mes "Ok, nothing to change.";
				close;
			}

			mes "[^FFA500Battle Recruiter^000000]";
			mes "Close this window to continue...";
			close2;
			bg_queue_leave getvariableofnpc(.BG_Queue,"BG_Queue_Join");
			end;
		case 4:
			mes "[^FFA500Battle Recruiter^000000]";
			mes "Tell me... what battleground you don't understand?";
			next;
			switch( select("Capture the Flag:Team DeathMatch:Stone Control:Eye of Storm:Bossnia:Domination:Triple Inferno:Conquest:Rush") )
			{
			case 1:
				mes "[^FFA500Battle Recruiter^000000]";
				mes "The objective of the Flavius Battle CTF is to score 3 points before your enemy, by capture their Flag.";
				next;
				mes "[^FFA500Battle Recruiter^000000]";
				mes "To capture a Flag you need to take the enemy flag, and bring it to your base flag.";
				next;
				mes "[^FFA500Battle Recruiter^000000]";
				mes "If your base flag is captured too, you need to kill the flag carrier and take the flag back to your base.";
				break;
			case 2:
				mes "[^FFA500Battle Recruiter^000000]";
				mes "Kill all the enemy players to let their Team without points.";
				mes "Protect our army.";
				break;
			case 3:
				mes "[^FFA500Battle Recruiter^000000]";
				mes "Take the Stones in the middle of the battlefield and put in on your base, in the Stone Points.";
				next;
				mes "[^FFA500Battle Recruiter^000000]";
				mes "Each Stone will give points to your team. First team reach 99 points wins the game.";
				next;
				mes "[^FFA500Battle Recruiter^000000]";
				mes "Protect your stones from to be captured by the enemy.";
				break;
			case 4:
				mes "[^FFA500Battle Recruiter^000000]";
				mes "There are two bases, North and South which teams can capture by holding position on the Base more than the other team.";
				next;
				mes "[^FFA500Battle Recruiter^000000]";
				mes "Each base will give you points each 5 seconds of Domination. If your Team control both bases the amount of points increases.";
				next;
				mes "[^FFA500Battle Recruiter^000000]";
				mes "To get aditional points, in the middle there is a Flag spawn, capture it and put it on any of your team Bases.";
				next;
				mes "[^FFA500Battle Recruiter^000000]";
				mes "The first Team reach 99 points wins the match.";
				break;
			case 5:
				mes "[^FFA500Battle Recruiter^000000]";
				mes "Attack the enemy base and destroy each MVP Guardian. To do damage to the guardian your team must capture the Balance Flag in the middle base.";
				next;
				mes "[^FFA500Battle Recruiter^000000]";
				mes "Each team have 5 guardian to be protected or killed.";
				break;
			case 6:
				mes "[^FFA500Battle Recruiter^000000]";
				mes "There are three bases, North, Center and South which teams can capture by holding position on the Base more than the other team.";
				next;
				mes "[^FFA500Battle Recruiter^000000]";
				mes "Each base will give you points each 5 seconds of Domination.";
				next;
				mes "[^FFA500Battle Recruiter^000000]";
				mes "The first Team reach 99 points wins the match.";
				break;
			case 7:
				mes "[^FFA500Battle Recruiter^000000]";
				mes "There are 3 teams in the battlefield, your team and other 2 enemies.";
				next;
				mes "[^FFA500Battle Recruiter^000000]";
				mes "Kill the enemy players, collect the skulls and bring then to the Sacrifice Totem to win points.";
				next;
				mes "[^FFA500Battle Recruiter^000000]";
				mes "You can collect your own team skulls and bring then to your Sacrifice Totem to avoid other teams to score.";
				next;
				mes "[^FFA500Battle Recruiter^000000]";
				mes "If you get killed all your skulls will be drop to the floor, including your own skull. First Team to get 80 points wins the battle.";
				break;
			case 8:
				mes "[^FFA500Battle Recruiter^000000]";
				mes "If you are Attacking, destroy the enemy defenses and it's Flag.";
				next;
				mes "[^FFA500Battle Recruiter^000000]";
				mes "If you are Defending, protect your castle defenses and the Flag.";
				break;
			case 9:
				mes "[^FFA500Battle Recruiter^000000]";
				mes "Fight to capture the Castle and organize your team to defend it.";
				next;
				mes "[^FFA500Battle Recruiter^000000]";
				mes "If you fail on the first capture, kill the defender and take it for your team.";
				break;
			}
			break;
		}
		close;
	case 2:
		mes "[^FFA500Battle Recruiter^000000]";
		mes "Free For All Arena is a Duel between 10 players with No Teams.";
		mes "The target is to kill 25 players.";
		mes "What do you want to do?";
		next;
		switch( select("Register:Leave Queue") )
		{
		case 1:
			if( BaseLevel < 80 )
			{
				mes "[^FFA500Battle Recruiter^000000]";
				mes "Min required level to join a battleground is 80.";
				close;
			}
			if( checkquest(8506,PLAYTIME) == 2 )
				erasequest 8506;
			if( checkquest(8506,PLAYTIME) != -1 )
			{
				mes "[^FFA500Battle Recruiter^000000]";
				mes "You are a Deserter. You can't participate until the indicator goes off";
				close;
			}

			mes "[^FFA500Battle Recruiter^000000]";
			mes "Let's proceed with the registration...";
			mes "You must wait on any City until BG starts.";
			mes "Close this window to continue...";
			close2;
			bg_queue_join getvariableofnpc(.VS_Queue,"BG_Queue_Join");
			end;
		case 2:
			mes "[^FFA500Battle Recruiter^000000]";
			mes "If you are registered, you will be removed.";
			mes "Are you sure?";
			next;
			if( select("Yes, leave queue:No, I will stay") == 2 )
			{
				mes "[^FFA500Battle Recruiter^000000]";
				mes "Ok, nothing to change.";
				close;
			}

			mes "[^FFA500Battle Recruiter^000000]";
			mes "Close this window to continue...";
			close2;
			bg_queue_leave getvariableofnpc(.VS_Queue,"BG_Queue_Join");
			end;
		}
		break;
	case 3:
		mes "[^FFA500Battle Recruiter^000000]";
		mes "May the War God bless you.";
		close2;
		warp "bat_room",155,150;
		end;
	}
	end;
}

bat_room,160,150,3	duplicate(BG_Register)	Battle Recruiter#bat	728
// prontera,155,180,4	duplicate(BG_Register)	Battle Recruiter#prt	728

// General Guillaume
// *********************************************************************
bat_room,160,159,3	script	General Guillaume	420,{
	cutin "bat_kiyom2",2;
	mes "[General Guillaume]";
	mes "Hot-blooded adventurer, we need your ability to win this battle.";
	next;
	cutin "bat_kiyom1",2;
	mes "[General Guillaume]";
	mes "Our great king, Marcel Marollo VII, is very sick lately. His Majesty has declared that he chosen either me or Prince Croix as the next king amongst his 9 sons.";
	next;
	mes "[General Guillaume]";
	mes "Two kings can't share a nation!";
	mes "Only the one victorious from His Majesty's appointed battle will be enthroned.";
	next;
	mes "[General Guillaume]";
	mes "This is however, not just a battle between us. This battle will determine the future of this country. I pledge on my honor to prove that I'm the one who can protect this Maroll from outside threats.";
	next;
	switch( select("Join the Blue Team","Join the Green Team","End Conversation") )
	{
	case 1:
		cutin "bat_kiyom2",2;
		mes "[General Guillaume]";
		mes "Welcome to my army, comrade.";
		mes "Your eyes tell me that you're a soldier that I can trust.";
		set Bat_Team,1;
		next;
		mes "[General Guillaume]";
		mes "Now, go upstairs and apply for battle with your comrades. I'm sure they'll welcome you whole-heartedly!";
		break;
	case 2:
		cutin "bat_kiyom2",2;
		mes "[General Guillaume]";
		mes "So you will take this fight as your own...";
		mes "Then you are my enemy.";
		set Bat_Team,0;
		next;
		mes "[General Guillaume]";
		mes "Go now, We will meet again in the Battlefield.";
		break;
	case 3:
		mes "[General Guillaume]";
		mes "I'll be the one who will capture the flag!";
		break;
	}

	close2;
	cutin "",255;
	end;
}

// General Croix
// *********************************************************************
bat_room,160,140,3	script	Prince Croix	416,{
	cutin "bat_crua1",2;
	mes "[Prince Croix]";
	mes "Wise adventurer, why don't you lend us your power for victory?";
	next;
	cutin "bat_crua2",2;
	mes "[Prince Croix]";
	mes "I do not wish to shed blood, but I have no choice but to fight for the possibility of peace and for the sake of my people.";
	next;
	mes "[Prince Croix]";
	mes "General Guillaume may have an advantage in this battle as he is the great general of Maroll, but that doesn't automatically mean he'll win. I want to win this battle so that I can grant a better future for my people.";
	next;
	
	switch( select("Join the Red Team","Join the Green Team","End Conversation") )
	{
	case 1:
		mes "[Prince Croix]";
		mes "Thank you so much. I feel like I can win with the help of adventurers like you. Now, please go downstairs and join your comrades in sharpening their skills to fight the enemy!";
		set Bat_Team,2;
		break;
	case 2:
		mes "[Prince Croix]";
		mes "So you will take this fight as your own...";
		mes "Then you are my enemy.";
		set Bat_Team,0;
		next;
		mes "[Prince Croix]";
		mes "Go now, We will meet again in the Battlefield.";
		break;
	case 3:
		mes "[Prince Croix]";
		mes "For Maroll!";
		break;
	}	
	
	close2;
	cutin "",255;
	end;
}

// Guard Dummy
// *********************************************************************

bat_room,161,141,3	script	Prince Croix's Aid::bat_aid	415,{ end; }
bat_room,161,139,3	duplicate(bat_aid)	Prince Croix's Aid::bat_aid2	415
bat_room,161,160,3	duplicate(bat_aid)	General Guillaume's Aid::bat_aid3	419
bat_room,161,158,3	duplicate(bat_aid)	General Guillaume's Aid::bat_aid4	419

// Flags
// *********************************************************************

-	script	Base Flag#bg	-1,{ end; }

// BattleGround Warper - Exit
// *********************************************************************

bat_room,148,150,4	script	Teleporter#bat	124,{
	mes "[Teleporter]";
	mes "Do you wish to leave the battlefield? Use my service to return to town.";
	next;
	if( select("Leave:Don't Leave") == 2 )
	{
		mes "[Teleporter]";
		mes "I'll be here whenever you're in need of my service.";
		close;
	}

	switch( @ArenaPVP_out )
	{
		// Rune Midgard Republic
		case 1: warp "prontera",149,92; break;
		case 2: warp "payon",165,98; break;
		case 3: warp "morocc",153,94; break;
		case 4: warp "umbala",121,143; break;
		case 5: warp "comodo",196,140; break;
		case 6: warp "niflheim",214,193; break;
		case 7: warp "aldebaran",143,111; break;
		case 8: warp "geffen",107,53; break;
		// Schwarzard Republic
		case 9: warp "yuno",151,177; break;
		case 10: warp "hugel",99,143; break;
		case 11: warp "lighthalzen",167,93; break;
		case 12: warp "einbroch",70,194; break;
		case 13: warp "einbech",168,130; break;
		// Arunafelz Republic
		case 14: warp "rachel",118,114; break;
		case 15: warp "veins",207,122; break;
		// Islands
		case 16: warp "nameless_n",161,179; break;
		case 17: warp "louyang",213,106; break;
		case 18: warp "gonryun",154,111; break;
		case 19: warp "ayothaya",148,109; break;
		case 20: warp "moscovia",229,195; break;
		case 21: warp "xmas",151,127; break;
		case 22: warp "amatsu",203,107; break;
		case 23: warp "izlude",126,114; break;
		case 24: warp "brasilis",195,211; break;
		case 25: warp "manuk",279,214; break;
		case 26: warp "splendide",200,174; break;
		// Anyplace else
		default:
			set .@spoint$, getsavepoint(0);
			set .@x, getsavepoint(1);
			set .@y, getsavepoint(2);
			mes "[Teleporter]";
			mes "You will be sent back to " + .@spoint$ + ".";
			close2;
			warp .@spoint$, .@x, .@y;
			break;
	}
	
	set @ArenaPVP_out, 0;
	end;
}

// Kafra
// *********************************************************************
bat_room,148,147,4	script	Kafra Staff::kaf_bat	861,{
	cutin "kafra_09",2;
	callfunc "F_Kafra",0,2,1,150,0;
}

// Badges Exchange
// *********************************************************************

bat_room,163,146,2	script	Telma	701,{
	mes "[Telma]";
	mes "Welcome, mighty warrior.";
	mes "Do you need supply for your battles?";
	mes "I can exchange you a nice amount for your badges or open the normal Chronos Supply Store for you.";
	next;
	switch( select("^FF0000Terra Old Red Box^000000:^FFA500Chronos Supply Store^000000:^FF0000Battleground Exclusive^000000:^0000FFWar of Emperium Exclusive Boxs:War of Emperium Exclusive Items^000000:Other Items") )
	{
	case 1: // Terra Old Red Box
		mes "[Telma]";
		mes "You can exchange your BG partipation points, a full round of each won BG, for one ^FF0000Terra Old Red Box^000000.";
		mes "Let me check your points...";
		next;
		mes "Tierra EoS : ^0000FF" + BG_EoE + "^000000";
		mes "Tierra Boss : ^0000FF" + BG_Boss + "^000000";
		mes "Tierra Dom : ^0000FF" + BG_DOM + "^000000";
		mes "Flavius CTF : ^0000FF" + BG_CTF + "^000000";
		mes "Flavius TDM : ^0000FF" + BG_TDM + "^000000";
		mes "Flavius SC : ^0000FF" + BG_SC + "^000000";
		mes "Conquest : ^0000FF" + BG_CON + "^000000";
		mes "Rush : ^0000FF" + BG_RUSH + "^000000";
		mes "Triple Inferno : ^0000FF" + BG_TI + "^000000";
		next;
		if( BG_EoE > 0 && BG_Boss > 0 && BG_CTF > 0 && BG_TDM > 0 && BG_SC > 0 && BG_TI > 0 && BG_CON > 0 && BG_RUSH > 0 && BG_DOM > 0 )
		{
			set BG_EoE, BG_EoE - 1;
			set BG_Boss, BG_Boss - 1;
			set BG_DOM, BG_DOM - 1;
			set BG_CTF, BG_CTF - 1;
			set BG_TDM, BG_TDM - 1;
			set BG_SC, BG_SC - 1;
			set BG_CON, BG_CON - 1;
			set BG_RUSH, BG_RUSH - 1;
			set BG_TI, BG_TI - 1;
			getitem 5000,1;
			mes "[Telma]";
			mes "Here is one Box for you!!";
			mes "I hope you can find inside it a big surprise.";
			mes "Talk to me if you want to exchange more.";
		}
		else
		{
			mes "[Telma]";
			mes "To exchange your points for a Box, you need at least 2 one of each Battleground.";
			mes "Play all kinds of BG's!!.";
		}
		close;
	case 2: // Supply Store
		mes "[Telma]";
		mes "Close this window to open the Chronos Supply Store.";
		close2;
		callshop "ChronosShop",1;
		end;
	case 3: // BG Supply
		setarray .@Items[0],   547,  504,  505,  678, 7135, 7136, 7137, 7138, 7139,  715,  716,  717,12016, 1025, 7321,  662,12072,12077,12082,12087,12092,12097;
		setarray .@Amount[0],  150,  100,  100,   10,   40,   40,   30,   30,   25,   50,  100,  200,   10,   40,   25,   10,    3,    3,    3,    3,    3,    3;
		setarray .@Value[0],     1,    1,    1,    1,    1,    1,    1,    1,    1,    1,    1,    1,    1 ,   1,    1,    1,    1,    1,    1,    1,    1,    1;
		set .@cID, getbattleflag("bg_reserved_char_id");
		set .@Tarjet$, "Battleground";
		break;
	case 4: // WoE Supply Boxs
		setarray .@Items[0],  8916, 8917, 8924, 8929, 8935, 8934,             8926, 8927, 8962, 8961, 8925, 8928, 8930;
		setarray .@Amount[0],    3,    1,    2,    2,    1,    1,                2,    1,    1,    1,    4,    1,    1;
		setarray .@Value[0],     8,    4,    4,    8,   10,   10,                8,    8,    4,    2,    8,   10,    8;
		set .@cID, 0;
		set .@Tarjet$, "War of Emperium or GvG";
		break;
	case 5: // WoE Supply Items
		setarray .@Items[0],  7137, 7138,  662,12072,12077,12082,12087,12092,12097,14287,14288;
		setarray .@Amount[0],   30,   30,   10,    6,    6,    6,    6,    6,    6,    5,    5;
		setarray .@Value[0],     4,    4,    4,    8,    8,    8,    8,    8,    8,    8,    8;
		set .@cID, getbattleflag("woe_reserved_char_id");
		set .@Tarjet$, "War of Emperium or GvG";
		break;
	case 6:
		setarray .@Items[0],  8918, 8919, 8960;
		setarray .@Amount[0],    1,    1,    1;
		setarray .@Value[0],    15,   15,   15;
		set .@cID, 0;
		set .@Tarjet$, "Everywhere";
		break;
	}

	set .@Menu$, "";
	for( set .@i, 0; .@i < getarraysize(.@Items); set .@i, .@i + 1 )
		set .@Menu$, .@Menu$ + .@Amount[.@i] + " " + getitemname(.@Items[.@i]) + " (" + .@Value[.@i] + " badges):";
	set .@j, select(.@Menu$) - 1;

	mes "[Telma]";
	mes "^0000FF" + .@Amount[.@j] + " " + getitemname(.@Items[.@j]) + "^000000";
	mes "It will cost you " + .@Value[.@j] + " badges per pack.";
	mes "Remember this can only be used on " + .@Tarjet$ + ".";
	next;
	
	mes "[Telma]";
	mes "But tell me first, where do you want to receive the items?";
	next;
	set .@Destiny, select("Here, in my Inventory:Direct to my Storage") - 1;

	mes "[Telma]";
	mes "How many packets do you want to buy? (1..50)";
	next;

	input .@total;
	if( .@total < 1 || .@total > 50 )
	{
		mes "[Telma]";
		mes "I am sorry, please input a value from 1 to 50.";
		mes "Talk to me again when you do your selections.";
		close;
	}
	
	set .@vtotal, .@total * .@Value[.@j]; // Value of all Packages
	set .@total, .@total * .@Amount[.@j]; // Total Amount of Items

	if( .@Destiny )
	{ // Check Storage Space Limit
		if( .@cID )
		{
			if( checkspace(.@Items[.@j],.@total,1,0,0,254,0,.@cID&0xffff,(.@cID>>16)&0xffff) == 0 )
			{
				mes "[Telma]";
				mes "It's seen your storage cannot accept more items.";
				mes "Go to your store and check the free space.";
				close;
			}
		}
		else
		{
			if( checkspace(.@Items[.@j],.@total,1,0,0,0,0,0,0) == 0 )
			{
				mes "[Telma]";
				mes "It's seen your storage cannot accept more items.";
				mes "Go to your store and check the free space.";
				close;
			}
		}
	}
	else
	{ // Check Weight Limit
		if( checkweight(.@Items[.@j],.@total) == 0 )
		{
			mes "[Telma]";
			mes "It's seen you can't carry the stuff you want to buy.";
			mes "Go and store some items on your Kafra Storage.";
			close;
		}
	}

	if( countitem(7828) + countitem(7829) + countitem(7773) < .@vtotal )
	{
		mes "[Telma]";
		mes "Required Badges: ^0000FF" + .@vtotal + "^000000";
		mes "You do not have enough badges. Sorry...";
		mes "Participate in Battleground arenas to receive it.";
		close;
	}
	set .@vcount, .@vtotal;
	set .@i7828, 0;
	set .@i7829, 0;
	set .@i7773, 0;

	// Bravery Badges
	if( countitem(7828) > 0 )
	{
		mes "[Telma]";
		mes "Required Badges: ^0000FF" + .@vcount + "^000000";
		mes "Bravery Badges to expend:";
		next;
		input .@i7828;
		if( .@i7828 < 0 || .@i7828 > countitem(7828) )
		{
			mes "[Telma]";
			mes "Invalid number... Sorry.";
			close;
		}
		
		if( .@i7828 > .@vcount ) set .@i7828,.@vcount;
		set .@vcount, .@vcount - .@i7828;
	}

	// Valor Badges
	if( .@vcount && countitem(7829) > 0 )
	{
		mes "[Telma]";
		mes "Required Badges: ^0000FF" + .@vcount + "^000000";
		mes "Valor Badges to expend:";
		next;
		input .@i7829;
		if( .@i7829 < 0 || .@i7829 > countitem(7829) )
		{
			mes "[Telma]";
			mes "Invalid number... Sorry.";
			close;
		}
		
		if( .@i7829 > .@vcount ) set .@i7829,.@vcount;
		set .@vcount, .@vcount - .@i7829;
	}

	// Heroism Badges
	if( .@vcount && countitem(7773) > 0 )
	{
		mes "[Telma]";
		mes "Required Badges: ^0000FF" + .@vcount + "^000000";
		mes "Heroism Badges to expend:";
		next;
		input .@i7773;
		if( .@i7773 < 0 || .@i7773 > countitem(7773) )
		{
			mes "[Telma]";
			mes "Invalid number... Sorry.";
			close;
		}

		if( .@i7773 > .@vcount ) set .@i7773, .@vcount;
		set .@vcount, .@vcount - .@i7773;
	}

	// Final Checks
	if( .@vcount != 0 )
	{
		mes "[Telma]";
		mes "Sorry, not enough badges to pay the total value.";
		close;
	}

	mes "[Telma]";
	mes "Are you ready to pay and receive your items?";
	mes "Your payment details:";
	mes "^0000FF" + .@i7828 + "^000000 Bravery Badges";
	mes "^0000FF" + .@i7829 + "^000000 Valor Badges";
	mes "^0000FF" + .@i7773 + "^000000 Heroism Badges";
	next;

	if( select("Yes, let's do it:I am sorry... not this time") == 2 )
	{
		mes "[Telma]";
		mes "Ok, come again if you change your mind.";
		close;
	}

	if( .@Destiny )
	{ // Check Storage Space Limit
		if( .@cID )
		{
			if( checkspace(.@Items[.@j],.@total,1,0,0,254,0,.@cID&0xffff,(.@cID>>16)&0xffff) == 0 )
			{
				mes "[Telma]";
				mes "It's seen your storage cannot accept more items.";
				mes "Go to your store and check the free space.";
				close;
			}
		}
		else
		{
			if( checkspace(.@Items[.@j],.@total,1,0,0,0,0,0,0) == 0 )
			{
				mes "[Telma]";
				mes "It's seen your storage cannot accept more items.";
				mes "Go to your store and check the free space.";
				close;
			}
		}
	}
	else
	{ // Check Weight Limit
		if( checkweight(.@Items[.@j],.@total) == 0 )
		{
			mes "[Telma]";
			mes "It's seen you can't carry the stuff you want to buy.";
			mes "Go and store some items on your Kafra Storage.";
			close;
		}
	}

	if( .@i7828 > countitem(7828) || .@i7829 > countitem(7829) || .@i7773 > countitem(7773) )
	{
		mes "[Telma]";
		mes "What!?, are you a magician or a thief? What did you do with your badges!?";
		mes "Get out of here!";
		close;
	}

	delitem 7828,.@i7828;
	delitem 7829,.@i7829;
	delitem 7773,.@i7773;

	if( .@cID )
	{
		if( .@Destiny )
			storeitem2 .@Items[.@j],.@total,1,0,0,254,0,.@cID&0xffff,(.@cID>>16)&0xffff;
		else
			getitem2 .@Items[.@j],.@total,1,0,0,254,0,.@cID&0xffff,(.@cID>>16)&0xffff;
	}
	else
	{
		if( .@Destiny )
			storeitem .@Items[.@j],.@total;
		else
			getitem .@Items[.@j],.@total;
	}

	mes "[Telma]";
	mes "Here it is... Now take care and fight for the honor and glory of your prince.";
	close;
}

bat_room,1,1,3,7828	sshop	BraveryExchanger	111,13036:100,13411:100,1425:100,1632:100,1634:100,1543:100,1924:100,1978:100,1574:100,1824:100,1183:100,1380:100,13305:100,1279:100,1739:100,13108:100,13172:100,2538:50,2539:50,2540:50,2435:50,2436:50,2437:50,2376:80,2377:80,2378:80,2379:80,2380:80,2381:80,2382:80,2720:500,2721:500,2722:500,2723:500,2724:500,2725:500,2733:500
bat_room,1,1,3,7829	sshop	ValorExchanger	111,13037:100,13410:100,1633:100,1635:100,1542:100,1923:100,1977:100,1575:100,1823:100,1184:100,1482:100,1379:100,13306:100,1280:100,1738:100,13171:100,13173:100,13174:100,2538:50,2539:50,2540:50,2435:50,2436:50,2437:50,2376:80,2377:80,2378:80,2379:80,2380:80,2381:80,2382:80,2720:500,2721:500,2722:500,2723:500,2724:500,2725:500,2733:500
bat_room,1,1,3,7773	sshop	HeroismExchanger	111,1187:2000,1281:2000,1282:2000,1310:2000,1382:2000,1426:2000,1486:2000,1546:2000,1576:2000,1577:2000,1640:2000,1641:2000,1743:2000,1826:2000,1827:2000,1927:2000,1981:2000,2002:2000,13042:2000,13110:2000,13176:2000,13177:2000,13178:2000,13179:2000,13307:2000,13416:2000,13417:2000,13418:2000,2394:840,2395:170,2396:25,2444:580,2445:70,2446:15,2549:630,2772:1200,2773:280,2774:40

bat_room,163,154,2	script	Erundek	109,{
	mes "[Erundek]";
	mes "Welcome, mighty warrior.";
	mes "What can I do for you today ?";
	next;
	switch( select("Exchange Bravery Badges","Exchange Valor Badges","Exchange Heroism Badges","Hmm, nothing I guess.") )
	{
		case 4:
			mes "[Erundek]";
			mes "As you wish.";
			mes "See you later.";
			close;
		case 1: // Bravery Badges
			mes "[Erundek]";
			mes "So you want to exchange ^0000FFBravery Badges^000000 from the Battle of Tierra Valley.";
			mes "Close this window to open shop.";
			close2;
			callshop "BraveryExchanger",0;
			end;
		case 2: // Valor Badges
			mes "[Erundek]";
			mes "So you want to exchange ^FF0000Valor Badges^000000 from the Battle of Flavius.";
			mes "Close this window to open shop.";
			close2;
			callshop "ValorExchanger",0;
			end;
		case 3: // Heroism Badges
			mes "[Erundek]";
			mes "So you want to exchange ^FFA500Heroism Badges^000000 from the Battle of Kriger Von Midgard.";
			mes "Close this window to open shop.";
			close2;
			callshop "HeroismExchanger",0;
			end;
	}
}
