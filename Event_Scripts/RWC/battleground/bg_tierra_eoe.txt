// ==============================================================================
// BattleGround System - Tierra Valley Eye of Storm
// ==============================================================================

// Registration NPC's
// *********************************************************************

bat_room,85,97,4	script	Registration::EOSR_Guillaume	418,{
	end;
OnInit:
	waitingroom "Battle Station 15 Players",15,"Tierra_EOS::OnGuillaumeJoin",1;
	end;
OnEnterBG:
	set $@TierraEOS_id1, waitingroom2bg("bat_a02",50,374,0,"Tierra_EOS::OnGuillaumeQuit","Tierra_EOS::OnGuillaumeDie");
	end;
}

bat_room,85,74,0	script	Registration::EOSR_Croix	414,{
	end;
OnInit:
	waitingroom "Battle Station 15 Players",15,"Tierra_EOS::OnCroixJoin",1;
	end;
OnEnterBG:
	set $@TierraEOS_id2, waitingroom2bg("bat_a02",42,16,1,"Tierra_EOS::OnCroixQuit","Tierra_EOS::OnCroixDie");
	end;
}

// Battleground Engine
// *********************************************************************

-	script	Tierra_EOS_Announcer	-1,{
	end;

OnStart:
	initnpctimer;
	end;

OnTimer10000:
	stopnpctimer;
	if( $@TierraEOS != 1 )
		end;

	mapannounce "bat_a02","--- Guillaume " + getvariableofnpc(.Guillaume_Score,"Tierra_EOS") + " --- Croix Team " + getvariableofnpc(.Croix_Score,"Tierra_EOS") + " ---",1,0x00BFFF;
	initnpctimer;
	end;
}

-	script	Tierra_EOS	-1,{
	end;

OnInit:
	disablenpc "Therapist in battle#eoe1";
	disablenpc "Therapist in battle#eoe2";
	disablenpc "Guillaume Vintenar#eoe1";
	disablenpc "Croix Vintenar#eoe2";
	end;

OnGuillaumeJoin:
OnCroixJoin:
	if( $@TierraEOS == 0 )
		donpcevent "Tierra_EOS::OnReadyCheck";
	else if( $@TierraEOS == 1 )
		donpcevent "Tierra_EOS::OnDoBalance";
	end;

OnDoBalance:
	if( $@TierraEOS != 1 )
		end;

	set .@Guillaume, bg_get_data($@TierraEOS_id1, 0);
	set .@Croix, bg_get_data($@TierraEOS_id2, 0);

	if( .@Guillaume < .@Croix )
		waitingroom2bg_single $@TierraEOS_id1,"bat_a02",353,344,"EOSR_Guillaume";
	else if( .@Guillaume > .@Croix )
		waitingroom2bg_single $@TierraEOS_id2,"bat_a02",353,52,"EOSR_Croix";
	else
	{
		set .@GuillaumeR, getwaitingroomstate(0,"EOSR_Guillaume");
		set .@CroixR, getwaitingroomstate(0,"EOSR_Croix");

		if( .@GuillaumeR <= .@CroixR )
			set .@Limit, .@GuillaumeR;
		else
			set .@Limit, .@CroixR;

		if( .@Limit + .@Guillaume > 40 )
			set .@Limit, 40 - .@Guillaume;
		if( .@Limit <= 0 ) end;

		for( set .@i, 0; .@i < .@Limit; set .@i, .@i + 1 )
		{
			waitingroom2bg_single $@TierraEOS_id1,"bat_a02",353,344,"EOSR_Guillaume";
			waitingroom2bg_single $@TierraEOS_id2,"bat_a02",353,52,"EOSR_Croix";
		}

		set .@Guillaume, .@Guillaume + .@Limit;
		set .@Croix, .@Croix + .@Limit;
		mapannounce "bat_room","Battleground -- Tierra EoS [80-99] G: " + .@Guillaume + "/40, C: " + .@Croix + "/40 (Playing)",1,0x00BFFF;
		chatmessage "#bg : [Server] Tierra EoS [80-99] G: " + .@Guillaume + "/40, C: " + .@Croix + "/40 (Playing)", CHN_BATTLEGROUND, 1;
	}
	end;

OnReadyCheck:
	if( $@TierraEOS || $@Global_BG_Block )
		end;
	set .@Guillaume, getwaitingroomstate(0,"EOSR_Guillaume");
	set .@Croix, getwaitingroomstate(0,"EOSR_Croix");
	
	if( .@Guillaume < 15 || .@Croix < 15 )
	{
		if( .@Guillaume > 13 && .@Croix > 13 && !agitcheck() && $@TierraEOS_Flood < gettimetick(2) )
		{
			announce "Battleground -- Tierra EoS [80-99] G: " + .@Guillaume + "/15, C: " + .@Croix + "/15",0,0x00BFFF;
			set $@TierraEOS_Flood, gettimetick(2) + 15;
		}
		else
			mapannounce "bat_room","Battleground -- Tierra EoS [80-99] G: " + .@Guillaume + "/15, C: " + .@Croix + "/15",1,0x00BFFF;
		end;
	}

	set $@TierraEOS, 1;

	donpcevent "EOSR_Croix::OnEnterBG";
	donpcevent "EOSR_Guillaume::OnEnterBG";
	// BG Variables
	set $@TierraEOS_Victory, 0;
	set .Guillaume_Score, 0;
	set .Croix_Score, 0;
	announce "Battleground -- Tierra EoS [80-99] has started!",0,0x00BFFF;
	sleep 2000;
	bg_warp $@TierraEOS_id1,"bat_a02",353,344;
	bg_warp $@TierraEOS_id2,"bat_a02",353,52;
	sleep 3000;
	// Respawn NPC's
	donpcevent "#guieoe_respawn::OnBGStart";
	donpcevent "#croeoe_respawn::OnBGStart";
	// Therapist
	enablenpc "Therapist in battle#eoe1";
	enablenpc "Therapist in battle#eoe2";
	// Reward NPC's
	disablenpc "Guillaume Vintenar#eoe1";
	disablenpc "Croix Vintenar#eoe2";
	// Start Match!!
	donpcevent "North_Base::OnBase";
	donpcevent "South_Base::OnBase";
	donpcevent "::OnStartStorm";
	donpcevent "Tierra_EOS_Announcer::OnStart";
	sleep 5000;
	mapannounce "bat_a02","The Battle of Tierra Valley - Eye of Storm has begun",1,0x00BFFF;
	sleep 15000;
	donpcevent "Neutral_Flag::OnBase";
	end;

OnFlash:
	if( @Flag_Carrier && $@TierraEOS == 1 )
	{
		getmapxy .@m$, .@x, .@y, 0;
		viewpointmap "bat_a02",1,.@x,.@y,3,0xFFFF00;
		specialeffect2 73;
		emotion e_hlp,1;
		addtimer 2000, "Tierra_EOS::OnFlash";
		percentheal -5,-5;
	}
	end;

OnGuillaumeQuit:
	set BG_Delay_Tick, gettimetick(2) + 1200;
	if( $@TierraEOS == 1 )
		donpcevent "Tierra_EOS::OnDoBalance";
OnGuillaumeDie:
	if( $@TierraEOS == 1 && @Flag_Carrier )
	{ // Drop Flag
		set @Flag_Carrier, 0;
		pcblock 1,0; // UnBlock Skills/Items
		getmapxy .@m$, .@x, .@y, 0;
		movenpc "Neutral_Flag", .@x, .@y;
		mapannounce "bat_a02","Guillaume have droped the Flag",1,0xFFFFFF;
		sleep 2000;
		set getvariableofnpc(.Flag_Status,"Neutral_Flag"), 0; // OnFloor
		initnpctimer "Neutral_Flag";
		enablenpc "Neutral_Flag";
	}
	end;

OnCroixQuit:
	set BG_Delay_Tick, gettimetick(2) + 1200;
	if( $@TierraEOS == 1 )
		donpcevent "Tierra_EOS::OnDoBalance";
OnCroixDie:
	if( $@TierraEOS == 1 && @Flag_Carrier )
	{ // Drop Flag
		set @Flag_Carrier, 0;
		pcblock 1,0; // UnBlock Skills/Items
		getmapxy .@m$, .@x, .@y, 0;
		movenpc "Neutral_Flag", .@x, .@y;
		mapannounce "bat_a02","Croix have droped the Flag",1,0xFFFFFF;
		sleep 2000;
		set getvariableofnpc(.Flag_Status,"Neutral_Flag"), 0; // OnFloor
		initnpctimer "Neutral_Flag";
		enablenpc "Neutral_Flag";
	}
	end;

OnGuillaumeFlag:
	viewpointmap "bat_a02",2,0,0,3,0xFFFF00;
	set .@North, getvariableofnpc(.Owner,"North_Base");
	set .@South, getvariableofnpc(.Owner,"South_Base");

	if( .@North == .@South )
	{
		set .Guillaume_Score, .Guillaume_Score + 100;
		mapannounce "bat_a02","Guillaume Team captured the Flag [+100 puntos]",1,0x0000FF;
	}
	else
	{
		set .Guillaume_Score, .Guillaume_Score + 50;
		mapannounce "bat_a02","Guillaume Team captured the Flag [+50 puntos]",1,0x0000FF;
	}
	
	donpcevent "Tierra_EOS::OnValidateScore";
	sleep 20000;
	donpcevent "Neutral_Flag::OnBase";
	end;

OnCroixFlag:
	viewpointmap "bat_a02",2,0,0,3,0xFFFF00;
	set .@North, getvariableofnpc(.Owner,"North_Base");
	set .@South, getvariableofnpc(.Owner,"South_Base");
	
	if( .@North == .@South )
	{
		set .Croix_Score, .Croix_Score + 100;
		mapannounce "bat_a02","Croix Team captured the Flag [+100 puntos]",1,0xFF0000;
	}
	else
	{
		set .Croix_Score, .Croix_Score + 50;
		mapannounce "bat_a02","Croix Team captured the Flag [+50 puntos]",1,0xFF0000;
	}

	donpcevent "Tierra_EOS::OnValidateScore";
	sleep 20000;
	donpcevent "Neutral_Flag::OnBase";
	end;

OnScoreUpdate:
	if( set(.@North, getvariableofnpc(.Owner,"North_Base")) == 0 && set(.@South, getvariableofnpc(.Owner,"South_Base")) == 0 )
		end; // No owners

	if( .@North == .@South )
	{ // Double Domination
		if( .@North == 1 )
			set .Guillaume_Score, .Guillaume_Score + 5;
		else
			set .Croix_Score, .Croix_Score + 5;
	}
	else
	{
		if( .@North == 1 )
			set .Guillaume_Score, .Guillaume_Score + 1;
		else
			set .Croix_Score, .Croix_Score + 1;
		if( .@South == 1 )
			set .Guillaume_Score, .Guillaume_Score + 1;
		else
			set .Croix_Score, .Croix_Score + 1;
	}

	donpcevent "Tierra_EOS::OnValidateScore";
	end;

OnValidateScore:
	if( .Guillaume_Score < 2000 && .Croix_Score < 2000 )
		end;

	if( .Guillaume_Score >= 2000 && .Croix_Score >= 2000 )
	{ // Tie
		set $@TierraEOS_Victory, 3;
		mapannounce "bat_a02","The battle is over. This is a Tie...!",1,0x00BFFF;
		chatmessage "#bg : [Server] Tierra EoS [80-99] The battle is over. This is a Tie...", CHN_BATTLEGROUND, 1;
	}
	else if( .Guillaume_Score >= 2000 )
	{ // Guillaume Won
		set $@TierraEOS_Victory, 1;
		mapannounce "bat_a02","The Guillaume army has won the Battle of Tierra EoS!",1,0x0000FF;
		chatmessage "#bg : [Server] Tierra EoS [80-99] Guillaume Team Won the Match!", CHN_BATTLEGROUND, 1;
	}
	else if( .Croix_Score >= 2000 )
	{ // Croix Won
		set $@TierraEOS_Victory, 2;
		mapannounce "bat_a02","The Croix army has won the Battle of Tierra EoS!",1,0x0000FF;
		chatmessage "#bg : [Server] Tierra EoS [80-99] Croix Team Won the Match!", CHN_BATTLEGROUND, 1;
	}
	
	set $@TierraEOS, 2;
	donpcevent "Neutral_Flag::OnDisable";
	stopnpctimer "North_Base";
	stopnpctimer "South_Base";
	donpcevent "#guieoe_respawn::OnBGStop";
	donpcevent "#croeoe_respawn::OnBGStop";
	disablenpc "Therapist in battle#eoe1";
	disablenpc "Therapist in battle#eoe2";
	// Reward NPC's
	enablenpc "Guillaume Vintenar#eoe1";
	enablenpc "Croix Vintenar#eoe2";
	sleep 2000;
	bg_warp $@TierraEOS_id1,"bat_a02",50,374;
	bg_warp $@TierraEOS_id2,"bat_a02",42,16;
	sleep 3000;
	mapannounce "bat_a02","Battle of Tierra EoS will close in 1 minute!",1,0x00BFFF;
	initnpctimer;
	end;

OnTimer30000:
	mapannounce "bat_a02","Battle of Tierra EoS will close in 30 seconds!",1,0x00BFFF;
	end;
OnTimer50000:
	mapannounce "bat_a02","Battle of Tierra EoS will close in 10 seconds!",1,0x00BFFF;
	end;

OnTimer60000:
OnReset:
	stopnpctimer;
	// Variables
	set .Guillaume_Score, 0;
	set .Croix_Score, 0;
	set $@TierraEOS_Victory, 0;
	// NPC's
	donpcevent "Neutral_Flag::OnDisable";
	stopnpctimer "North_Base";
	stopnpctimer "South_Base";
	disablenpc "Guillaume Vintenar#eoe1";
	disablenpc "Croix Vintenar#eoe2";
	disablenpc "Therapist in battle#eoe1";
	disablenpc "Therapist in battle#eoe2";

	if( $@TierraEOS_id1 ) { bg_destroy $@TierraEOS_id1; set $@TierraEOS_id1, 0; }
	if( $@TierraEOS_id2 ) { bg_destroy $@TierraEOS_id2; set $@TierraEOS_id2, 0; }
	sleep 1000;
	mapwarp "bat_a02","bat_room",155,150;
	sleep 2000;
	maprespawnguildid "bat_a02",0,3; // Just in case someone else
	sleep 2000;
	set $@TierraEOS, 0;
	donpcevent "Tierra_EOS::OnReadyCheck";
	end;

OnBreak:
	if( $@TierraEOS != 1 )
		end;

	if( getcharid(4) == $@TierraEOS_id1 )
		mapannounce "bat_a02","Netraul Flag Taken by Guillaume Team",1,0x0000FF;
	else if( getcharid(4) == $@TierraEOS_id2 )
		mapannounce "bat_a02","Netraul Flag Taken by Croix Team",1,0xFF0000;
	else end;

	set getvariableofnpc(.Flag_Status,"Neutral_Flag"), 1;
	set @Flag_Carrier, 1;

	sc_end SC_HIDING;
	sc_end SC_CLOAKING;
	sc_end SC_CHASEWALK;
	pcblock 1,1; // Block Skills/Items

	stopnpctimer "Neutral_Flag";
	disablenpc "Neutral_Flag";

	addtimer 2000, "Tierra_EOS::OnFlash";
	end;
}

// Battleground Flag
// *********************************************************************

bat_a02,273,204,0	script	Neutral Flag::Neutral_Flag	1911,1,1,{
	end;

OnTouch:
	if( $@TierraEOS != 1 || HP < 1 || .Flag_Status )
		end;

	if( getcharid(4) == $@TierraEOS_id1 )
		mapannounce "bat_a02","Netraul Flag Taken by Guillaume Team",1,0x0000FF;
	else if( getcharid(4) == $@TierraEOS_id2 )
		mapannounce "bat_a02","Netraul Flag Taken by Croix Team",1,0xFF0000;
	else end;

	set .Flag_Status, 1;
	set @Flag_Carrier, 1;

	sc_end SC_HIDING;
	sc_end SC_CLOAKING;
	sc_end SC_CHASEWALK;
	pcblock 1,1; // Block Skills/Items

	disablenpc "Neutral_Flag";
	addtimer 2000, "Tierra_EOS::OnFlash";
	stopnpctimer;
	end;

OnTimer2000:
	stopnpctimer;
	if( .Flag_Status == 0 && $@TierraEOS == 1 )
	{
		getmapxy .@m$, .@x, .@y, 1;
		viewpointmap "bat_a02",1,.@x,.@y,3,0xFFFF00;
		specialeffect 223;
		initnpctimer;
	}
	end;

OnDisable:
	movenpc "Neutral_Flag",273,204;
	set .Flag_Status, 0;
	stopnpctimer;
	disablenpc "Neutral_Flag";
	killmonster "bat_a02","Tierra_EOS::OnBreak";
	end;

OnBase:
	if( $@TierraEOS != 1 )
		end;

	movenpc "Neutral_Flag",273,204;
	set .Flag_Status, 0;
	initnpctimer;
	disablenpc "Neutral_Flag";
	mapannounce "bat_a02","The Neutral Flag have been set!!",1,0xFFFFFF;
	bg_monster 0,"bat_a02",273,204,"Neutral Flag",1911,"Tierra_EOS::OnBreak";
	end;
}

// Battleground Bases
// *********************************************************************

bat_a02,173,345,0	script	North Base::North_Base	1911,3,3,{
	end;

OnTouch:
	if( $@TierraEOS != 1 || .Owner == 0 || .Owner != Bat_Team || !@Flag_Carrier )
		end;

	set @Flag_Carrier, 0;
	pcblock 1,0; // UnBlock Skills/Items
	if( .Owner == 1 )
		donpcevent "Tierra_EOS::OnGuillaumeFlag";
	else
		donpcevent "Tierra_EOS::OnCroixFlag";
	end;

OnBase:
	set .Owner, 0;
	set .Balance, 0;
	setnpcdisplay "North_Base","North Base",1911;
	initnpctimer;
	end;

OnTimer1000:
	stopnpctimer;
	if( $@TierraEOS != 1 )
		end;

	set .@Guillaume, bg_getareausers($@TierraEOS_id1,"bat_a02",136,329,186,361);
	set .@Croix, bg_getareausers($@TierraEOS_id2,"bat_a02",136,329,186,361);
	set .Balance, .Balance + set(.@Balance, .@Guillaume - .@Croix);

	if( .Balance < -50 )
		set .Balance, -50;
	else if( .Balance > 50 )
		set .Balance, 50;

	switch( .Owner )
	{
	case 0:
		if( .Balance == 50 )
		{
			set .Balance, 50;
			set .Owner, 1; // Guillaume
			setnpcdisplay "North_Base","Guillaume Base",1912;
			mapannounce "bat_a02","North Base captured by Guillaume Team",1,0x0000FF;
		}
		else if( .Balance == -50 )
		{
			set .Balance, -50;
			set .Owner, 2; // Croix
			setnpcdisplay "North_Base","Croix Base",1913;
			mapannounce "bat_a02","North Base captured by Croix Team",1,0xFF0000;
		}
		break;
	case 1:
		if( .Balance <= 0 )
		{ // Guillaume lost Control
			set .Owner, 0;
			setnpcdisplay "North_Base","North Base",1911;
			mapannounce "bat_a02","Guillaume Team lost control of the North Base",1,0x0000FF;
		}
		break;
	case 2:
		if( .Balance >= 0 )
		{ // Croix lost Control
			set .Owner, 0;
			setnpcdisplay "North_Base","North Base",1911;
			mapannounce "bat_a02","Croix Team lost control of the North Base",1,0xFF0000;
		}
		break;
	}

	if( .@Balance > 0 )
		specialeffect 236;
	else if( .@Balance < 0 )
		specialeffect 225;
	else if( .Owner == 1 )
		specialeffect 236;
	else if( .Owner == 2 )
		specialeffect 225;
	else
		specialeffect 223;
	
	switch( .Owner )
	{
	case 0: viewpointmap "bat_a02",1, 173, 345, 1, 0xFFFFFF; break;
	case 1: viewpointmap "bat_a02",1, 173, 345, 1, 0x0000FF; break;
	case 2: viewpointmap "bat_a02",1, 173, 345, 1, 0xFF0000; break;
	}
	
	initnpctimer;
	end;
}

bat_a02,164,50,0	script	South Base::South_Base	1911,3,3,{
	end;

OnTouch:
	if( $@TierraEOS != 1 || .Owner == 0 || .Owner != Bat_Team || !@Flag_Carrier )
		end;

	set @Flag_Carrier, 0;
	pcblock 1,0; // UnBlock Skills/Items
	if( .Owner == 1 )
		donpcevent "Tierra_EOS::OnGuillaumeFlag";
	else
		donpcevent "Tierra_EOS::OnCroixFlag";
	end;

OnBase:
	set .Owner, 0;
	set .Balance, 0;
	setnpcdisplay "South_Base","South Base",1911;
	initnpctimer;
	end;

OnTimer1000:
	stopnpctimer;
	if( $@TierraEOS != 1 )
		end;

	set .@Guillaume, bg_getareausers($@TierraEOS_id1,"bat_a02",129,34,175,65);
	set .@Croix, bg_getareausers($@TierraEOS_id2,"bat_a02",129,34,175,65);
	set .Balance, .Balance + set(.@Balance, .@Guillaume - .@Croix);

	if( .Balance < -50 )
		set .Balance, -50;
	else if( .Balance > 50 )
		set .Balance, 50;

	switch( .Owner )
	{
	case 0:
		if( .Balance == 50 )
		{
			set .Balance, 50;
			set .Owner, 1; // Guillaume
			setnpcdisplay "South_Base","Guillaume Base",1912;
			mapannounce "bat_a02","South Base captured by Guillaume Team",1,0x0000FF;
		}
		else if( .Balance == -50 )
		{
			set .Balance, -50;
			set .Owner, 2; // Croix
			setnpcdisplay "South_Base","Croix Base",1913;
			mapannounce "bat_a02","South Base captured by Croix Team",1,0xFF0000;
		}
		break;
	case 1:
		if( .Balance <= 0 )
		{ // Guillaume lost Control
			set .Owner, 0;
			setnpcdisplay "South_Base","North Base",1911;
			mapannounce "bat_a02","Guillaume Team lost control of the South Base",1,0x0000FF;
		}
		break;
	case 2:
		if( .Balance >= 0 )
		{ // Croix lost Control
			set .Owner, 0;
			setnpcdisplay "South_Base","North Base",1911;
			mapannounce "bat_a02","Croix Team lost control of the South Base",1,0xFF0000;
		}
		break;
	}

	if( .@Balance > 0 )
		specialeffect 236;
	else if( .@Balance < 0 )
		specialeffect 225;
	else if( .Owner == 1 )
		specialeffect 236;
	else if( .Owner == 2 )
		specialeffect 225;
	else
		specialeffect 223;
	
	switch( .Owner )
	{
	case 0: viewpointmap "bat_a02",1,164,50,2,0xFFFFFF; break;
	case 1: viewpointmap "bat_a02",1,164,50,2,0x0000FF; break;
	case 2: viewpointmap "bat_a02",1,164,50,2,0xFF0000; break;
	}
	
	donpcevent "Tierra_EOS::OnScoreUpdate";
	initnpctimer;
	end;
}

// Battleground Therapist
// *********************************************************************

bat_a02,53,377,3	script	Therapist in battle#eoe2	95,{
	mes "[Therapist in battle]";
	mes "Just close your eyes, and take a deep breathe.";
	mes "You can be free from pain.";
	specialeffect2 312;
	close;
}

bat_a02,45,18,3	script	Therapist in battle#eoe1	95,{
	mes "[Therapist in battle]";
	mes "Just close your eyes, and take a deep breathe.";
	mes "You can be free from pain.";
	specialeffect2 312;
	close;
}

// Battleground Respawn
// *********************************************************************

bat_a02,50,374,0	script	#guieoe_respawn	139,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	misceffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_a02",46,370,54,378,100,100;
	if( $@Tierra_Base == 1 )
		areawarp "bat_a02",46,370,54,378,"bat_a02",285,226;
	else
		areawarp "bat_a02",46,370,54,378,"bat_a02",354,340;
	initnpctimer;
	end;
}

bat_a02,42,16,0	script	#croeoe_respawn	139,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	misceffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_a02",38,12,47,21,100,100;
	if( $@Tierra_Base == 2 )
		areawarp "bat_a02",38,12,47,21,"bat_a02",260,183;
	else
		areawarp "bat_a02",38,12,47,21,"bat_a02",354,57;
	initnpctimer;
	end;
}

// Battleground rewards
// *********************************************************************

bat_a02,53,377,3	script	Guillaume Vintenar#eoe1	419,{
	if( $@TierraEOS_Victory )
	{
		if( $@TierraEOS_Victory == Bat_Team )
		{ // Victory
			mes "[Swandery]";
			mes "Blessed Guillaume!!";
			mes "Let's enjoy our glorious victory!";
			mes "" + strcharinfo(0) + ", its a sign reflecting victory";
			close2;
			set .@Reward, 9;
		}
		else
		{ // 
			mes "[Swandery]";
			mes "You lost, but you're dedicated to this battle.";
			mes "This is a reward for your great dedication by Guillaume Marollo!";
			mes "Just take this defeat a lesson, and later you would definitely learn.";
			close2;
			set .@Reward, 3;
		}

		set @Flag_Carrier, 0;
		pcblock 1,0; // UnBlock Skills/Items

		set Tierra_EOS_Tick, gettimetick(2) + 300;
		// getitem 7828, .@Reward;
		bg_leave;
		warp "bat_room",155,150;
		end;
	}
	end;
}

bat_a02,45,19,3	script	Croix Vintenar#eoe2	415,{
	if( $@TierraEOS_Victory )
	{
		if( $@TierraEOS_Victory == Bat_Team )
		{ // Victory
			mes "[Swandery]";
			mes "Blessed Croax!!";
			mes "Let's enjoy our glorious victory!";
			mes "" + strcharinfo(0) + ", its a sign reflecting victory";
			close2;
			set .@Reward, 9;
		}
		else
		{ // 
			mes "[Swandery]";
			mes "Oh, " + strcharinfo(0) + ". Don't be sad.";
			mes "Even though we didn't win, we did our best.";
			mes "This is a Royal gift from Croix, and please don't forget this battle. We can win the next.";
			close2;
			set .@Reward, 3;
		}

		set @Flag_Carrier, 0;
		pcblock 1,0; // UnBlock Skills/Items

		set Tierra_EOS_Tick, gettimetick(2) + 300;
		// getitem 7828, .@Reward;
		bg_leave;
		warp "bat_room",155,150;
		end;
	}
	end;
}

// MapFlags
// *********************************************************************

bat_a02	mapflag	battleground
bat_a02	mapflag	nomemo
bat_a02	mapflag	nosave	SavePoint
bat_a02	mapflag	noteleport
bat_a02	mapflag	nowarp
bat_a02	mapflag	nowarpto
bat_a02	mapflag	noreturn
bat_a02	mapflag	nobranch
bat_a02	mapflag	nopenalty

// Eye of the Storm Effects
// *********************************************************************

bat_a02,269,189,0	script	#stormef1	139,{
	end;

OnStartStorm:
	if( $@TierraEOS != 1 ) end;

	sleep (rand(15,40) * 1000);
	specialeffect 622;
	specialeffect 537;

	goto OnStartStorm;
	end;
}

bat_a02,284,189,0	duplicate(#stormef1)	#stormef2	139
bat_a02,267,204,0	duplicate(#stormef1)	#stormef3	139
bat_a02,278,210,0	duplicate(#stormef1)	#stormef4	139
bat_a02,262,210,0	duplicate(#stormef1)	#stormef5	139
bat_a02,256,201,0	duplicate(#stormef1)	#stormef6	139
bat_a02,284,218,0	duplicate(#stormef1)	#stormef7	139
bat_a02,263,220,0	duplicate(#stormef1)	#stormef8	139
bat_a02,289,207,0	duplicate(#stormef1)	#stormef9	139
bat_a02,279,182,0	duplicate(#stormef1)	#stormef10	139
bat_a02,272,229,0	duplicate(#stormef1)	#stormef11	139
