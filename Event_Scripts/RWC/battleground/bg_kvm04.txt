// ==============================================================================
// BattleGround System - KvM 80~99
// ==============================================================================

// Light Effects
// *********************************************************************

bat_c04,54,123,0	script	#bat_c04a	111,{
	end;

OnKvM04LightA: misceffect 54; end;
OnKvM04LightB: misceffect 55; end;
OnKvM04LightC: misceffect 56; end;
}

bat_c04,56,125,0	duplicate(#bat_c04a)	#bat_c04b	111
bat_c04,58,127,0	duplicate(#bat_c04a)	#bat_c04c	111
bat_c04,141,56,0	duplicate(#bat_c04a)	#bat_c04d	111
bat_c04,143,58,0	duplicate(#bat_c04a)	#bat_c04e	111
bat_c04,145,60,0	duplicate(#bat_c04a)	#bat_c04f	111

// Battleground Engine
// *********************************************************************

-	script	KvM04_BG	-1,{
	end;

OnInit:
	setwall "bat_c04",54,122,6,7,0,"batc04wall_a";
	setwall "bat_c04",55,122,5,7,0,"batc04wall_b";
	setwall "bat_c04",140,56,6,7,0,"batc04wall_c";
	setwall "bat_c04",140,57,5,7,0,"batc04wall_d";
	disablenpc "TherapistKvM04a";
	disablenpc "TherapistKvM04b";
	disablenpc "VintenarKvM04a";
	disablenpc "VintenarKvM04b";
	end;

OnGuillaumeJoin:
	if( $@KvM04BG == 1 && bg_get_data($@KvM04BG_id1, 0) < 7 )
		waitingroom2bg_single $@KvM04BG_id1,"bat_c04",53,128,"KvM04R_Guillaume";
	else
		donpcevent "KvM04_BG::OnReadyCheck";
	end;

OnGuillaumeQuit:
	set BG_Delay_Tick, gettimetick(2) + 1200;
	if( $@KvM04BG == 1 && bg_get_data($@KvM04BG_id1, 0) < 7 )
		waitingroom2bg_single $@KvM04BG_id1,"bat_c04",53,128,"KvM04R_Guillaume";
OnGuillaumeDie:
	if( $@KvM04BG == 2 )
	{
		set .Guillaume_Count, .Guillaume_Count - 1;
		set .Croix_Score, .Croix_Score + 1;
		bg_updatescore "bat_c04",.Guillaume_Score,.Croix_Score;
		if( .Guillaume_Count < 1 ) donpcevent "KvM04_BG::OnCroixWin";
	}
	end;

OnCroixJoin:
	if( $@KvM04BG == 1 && bg_get_data($@KvM04BG_id2, 0) < 7 )
		waitingroom2bg_single $@KvM04BG_id2,"bat_c04",146,55,"KvM04R_Croix";
	else
		donpcevent "KvM04_BG::OnReadyCheck";
	end;

OnCroixQuit:
	set BG_Delay_Tick, gettimetick(2) + 1200;
	if( $@KvM04BG == 1 && bg_get_data($@KvM04BG_id2, 0) < 7 )
		waitingroom2bg_single $@KvM04BG_id2,"bat_c04",146,55,"KvM04R_Croix";
OnCroixDie:
	if( $@KvM04BG == 2 )
	{
		set .Croix_Count, .Croix_Count - 1;
		set .Guillaume_Score, .Guillaume_Score + 1;
		bg_updatescore "bat_c04",.Guillaume_Score,.Croix_Score;
		if( .Croix_Count < 1 ) donpcevent "KvM04_BG::OnGuillaumeWin";
	}
	end;

OnReadyCheck:
	if( $@KvM04BG || $@Global_BG_Block )
		end;
	set .@Guillaume, getwaitingroomstate(0,"KvM04R_Guillaume");
	set .@Croix, getwaitingroomstate(0,"KvM04R_Croix");

	if( .@Guillaume < 7 || .@Croix < 7 )
		end;

	set $@KvM04BG, 1; // Starting
	donpcevent "KvM04R_Croix::OnEnterBG";
	donpcevent "KvM04R_Guillaume::OnEnterBG";
	donpcevent "KvM04_BG::OnStart";
	end;

OnStart:
	announce "Battleground -- A new GvG Duel has started!",0,0x696969;
	enablenpc "TherapistKvM04a";
	enablenpc "TherapistKvM04b";
	disablenpc "VintenarKvM04a";
	disablenpc "VintenarKvM04b";
	set $@KvM04BG_Victory, 0;
	sleep 2000;
	// Warp Teams
	bg_warp $@KvM04BG_id1,"bat_c04",53,128;
	bg_warp $@KvM04BG_id2,"bat_c04",146,55;
	// ScoreBoard
	set .Guillaume_Score, 0;
	set .Croix_Score, 0;
	initnpctimer;
	end;

OnTimer5000:
	areapercentheal "bat_c04",50,123,58,131,100,100;
	areapercentheal "bat_c04",141,52,149,60,100,100;
	mapannounce "bat_c04","The Battle will start in 25 seconds!!",1,0x696969;
	end;

OnTimer26000:
	mapannounce "bat_c04","The Battle will start in 4 seconds!!",1,0x696969;
	end;

OnTimer27000:
	donpcevent "::OnKvM04LightA";
	end;

OnTimer28000:
	donpcevent "::OnKvM04LightB";
	end;
	
OnTimer29000:
	donpcevent "::OnKvM04LightC";
	end;

OnTimer30000:
	// Team Members
	set .Guillaume_Count, bg_get_data($@KvM04BG_id1, 0);
	set .Croix_Count, bg_get_data($@KvM04BG_id2, 0);
	if( .Guillaume_Count < 7 || .Croix_Count < 7 )
	{
		set $@KvM04BG_Victory, 3;
		set $@KvM04BG, 3;
		mapannounce "bat_c04","There are not enough players to start the battle",1,0x696969;
		stopnpctimer;
		sleep 2000;
		donpcevent "KvM04_BG::OnStop";
		end;
	}

	set $@KvM04BG, 2; // Playing
	areapercentheal "bat_c04",50,123,58,131,100,100;
	bg_warp $@KvM04BG_id1,"bat_c04",62,119;
	areapercentheal "bat_c04",141,52,149,60,100,100;
	bg_warp $@KvM04BG_id2,"bat_c04",137,64;
	end;

OnTimer32000:
	mapannounce "bat_c04","The Battle of Kreiger Von Midgard has begun!!",1,0x696969;
	end;

OnTimer420000:
	mapannounce "bat_c04","The Battle will ends in 30 seconds!!",1,0x696969;
	end;

OnTimer450000:
	if( .Croix_Count > .Guillaume_Count )
		donpcevent "KvM04_BG::OnCroixWin";
	else if( .Croix_Count < .Guillaume_Count )
		donpcevent "KvM04_BG::OnGuillaumeWin";
	else
	{ // Draw Game
		set $@KvM04BG, 3;
		set $@KvM04BG_Victory, 3;
		bg_set_data $@KvM04BG_id1,BG_KVM,BG_TIE,1;
		bg_set_data $@KvM04BG_id2,BG_KVM,BG_TIE,1;

		stopnpctimer;
		sleep 2000;
		mapannounce "bat_c04","The time is out! This is a Tie...",1,0x696969;
		donpcevent "KvM04_BG::OnStop";
	}
	end;

OnGuillaumeWin:
	set $@KvM04BG, 3;
	set $@KvM04BG_Victory, 1;
	bg_set_data $@KvM04BG_id1,BG_KVM,BG_W0N,1;
	bg_set_data $@KvM04BG_id2,BG_KVM,BG_LOST,1;
	
	stopnpctimer;
	sleep 2000;
	mapannounce "bat_c04","The Guillaume Army has won the Battle of Kreiger Von Midgard",1,0x0000FF;
	donpcevent "KvM04_BG::OnStop";
	end;

OnCroixWin:
	set $@KvM04BG, 3;
	set $@KvM04BG_Victory, 2;
	bg_set_data $@KvM04BG_id2,BG_KVM,BG_W0N,1;
	bg_set_data $@KvM04BG_id1,BG_KVM,BG_LOST,1;

	stopnpctimer;
	sleep 2000;
	mapannounce "bat_c04","The Croix Army has won the Battle of Kreiger Von Midgard",1,0xFF0000;
	donpcevent "KvM04_BG::OnStop";
	end;

OnStop:
	disablenpc "TherapistKvM04a";
	disablenpc "TherapistKvM04b";
	enablenpc "VintenarKvM04a";
	enablenpc "VintenarKvM04b";
	// Warp Teams
	bg_warp $@KvM04BG_id1,"bat_c04",53,128;
	bg_warp $@KvM04BG_id2,"bat_c04",146,55;
	donpcevent "KvM04_BG_Out::OnBegin";
	end;

OnReset:
	stopnpctimer;
	stopnpctimer "KvM04_BG_Out";
	set .Croix_Count, 0;
	set .Guillaume_Count, 0;
	set .Croix_Score, 0;
	set .Guillaume_Score, 0;
	set $@KvM04BG_Victory, 0;
	if( $@KvM04BG_id1 ) { bg_destroy $@KvM04BG_id1; set $@KvM04BG_id1, 0; }
	if( $@KvM04BG_id2 ) { bg_destroy $@KvM04BG_id2; set $@KvM04BG_id2, 0; }
	disablenpc "TherapistKvM04a";
	disablenpc "TherapistKvM04b";
	disablenpc "VintenarKvM04a";
	disablenpc "VintenarKvM04b";
	sleep 1000;
	mapwarp "bat_c04","bat_room",155,150;
	sleep 2000;
	maprespawnguildid "bat_c04",0,3; // Just in case someone else
	bg_updatescore "bat_c04",0,0;
	sleep 2000;
	set $@KvM04BG, 0;
	donpcevent "KvM04_BG::OnReadyCheck"; // Maybe a game is ready to start
	end;
}

-	script	KvM04_BG_Out	-1,{
	end;

OnBegin:
	initnpctimer;
	end;

OnTimer3000:
	mapannounce "bat_c04","Battle of Kreiger Von Midgard will close in 1 minute!",1,0x696969;
	end;
OnTimer30000:
	mapannounce "bat_c04","Battle of Kreiger Von Midgard will close in 30 seconds!",1,0x696969;
	end;
OnTimer50000:
	mapannounce "bat_c04","Battle of Kreiger Von Midgard will close in 10 seconds!",1,0x696969;
	end;
OnTimer60000:
	donpcevent "KvM04_BG::OnReset";
	end;
}

// Battleground Therapist
// *********************************************************************

bat_c04,51,130,5	script	Therapist in battle::TherapistKvM04a	95,{
	mes "[Therapist in battle]";
	mes "Just close your eyes, and take a deep breathe.";
	mes "You can be free from pain.";
	specialeffect2 312;
	close;
}

bat_c04,148,53,1	script	Therapist in battle::TherapistKvM04b	95,{
	mes "[Therapist in battle]";
	mes "Just close your eyes, and take a deep breathe.";
	mes "You can be free from pain.";
	specialeffect2 312;
	close;
}

// Battleground rewards
// *********************************************************************

bat_c04,51,130,5	script	Guillaume Vintenar::VintenarKvM04a	419,{
	if( $@KvM04BG_Victory )
	{
		if( $@KvM04BG_Victory == 3 )
		{
			set KvM_BG_Tick, gettimetick(2) + 30;
			bg_leave;
			warp "bat_room",155,150;
			end;
		}
		else if( $@KvM04BG_Victory == Bat_Team )
		{ // Victory
			set .@Reward, 5;
			mes "[Swandery]";
			mes "Blessed Guillaume!!";
			mes "Let's enjoy our glorious victory!";
			mes "" + strcharinfo(0) + ", its a sign reflecting victory";
			close2;
		}
		else
		{ // 
			set .@Reward, 1;
			mes "[Swandery]";
			mes "You lost, but you're dedicated to this battle.";
			mes "This is a reward for your great dedication by Guillaume Marollo!";
			mes "Just take this defeat a lesson, and later you would definitely learn.";
			close2;
		}

		set KvM_BG_Tick, gettimetick(2) + 30;
		// getitem 7773, .@Reward;
		bg_leave;
		warp "bat_room",155,150;
		end;
	}
	end;
}

bat_c04,148,53,1	script	Croix Vintenar::VintenarKvM04b	415,{
	if( $@KvM04BG_Victory )
	{
		if( $@KvM04BG_Victory == 3 )
		{
			set KvM_BG_Tick, gettimetick(2) + 30;
			bg_leave;
			warp "bat_room",155,150;
			end;
		}
		else if( $@KvM04BG_Victory == Bat_Team )
		{ // Victory
			set .@Reward, 5;
			mes "[Swandery]";
			mes "Blessed Croax!!";
			mes "Let's enjoy our glorious victory!";
			mes "" + strcharinfo(0) + ", its a sign reflecting victory";
			close2;
		}
		else
		{ // 
			set .@Reward, 1;
			mes "[Swandery]";
			mes "Oh, " + strcharinfo(0) + ". Don't be sad.";
			mes "Even though we didn't win, we did our best.";
			mes "This is a Royal gift from Croix, and please don't forget this battle. We can win the next.";
			close2;
		}

		set KvM_BG_Tick, gettimetick(2) + 30;
		// getitem 7773, .@Reward;
		bg_leave;
		warp "bat_room",155,150;
		end;
	}
	end;
}

// MapFlags
// *********************************************************************

bat_c04	mapflag	battleground	2
bat_c04	mapflag	nomemo
bat_c04	mapflag	nosave	SavePoint
bat_c04	mapflag	noteleport
bat_c04	mapflag	nowarp
bat_c04	mapflag	nowarpto
bat_c04	mapflag	noreturn
bat_c04	mapflag	nobranch
bat_c04	mapflag	nopenalty
