// ==============================================================================
// BattleGround System - KvM 80~99
// ==============================================================================

// Light Effects
// *********************************************************************

bat_c08,54,123,0	script	#bat_c08a	111,{
	end;

OnKvM08LightA: misceffect 54; end;
OnKvM08LightB: misceffect 55; end;
OnKvM08LightC: misceffect 56; end;
}

bat_c08,56,125,0	duplicate(#bat_c08a)	#bat_c08b	111
bat_c08,58,127,0	duplicate(#bat_c08a)	#bat_c08c	111
bat_c08,141,56,0	duplicate(#bat_c08a)	#bat_c08d	111
bat_c08,143,58,0	duplicate(#bat_c08a)	#bat_c08e	111
bat_c08,145,60,0	duplicate(#bat_c08a)	#bat_c08f	111

// Battleground Engine
// *********************************************************************

-	script	KvM08_BG	-1,{
	end;

OnInit:
	setwall "bat_c08",54,122,6,7,0,"batc08wall_a";
	setwall "bat_c08",55,122,5,7,0,"batc08wall_b";
	setwall "bat_c08",140,56,6,7,0,"batc08wall_c";
	setwall "bat_c08",140,57,5,7,0,"batc08wall_d";
	disablenpc "TherapistKvM08a";
	disablenpc "TherapistKvM08b";
	disablenpc "VintenarKvM08a";
	disablenpc "VintenarKvM08b";
	end;

OnGuillaumeJoin:
	if( $@KvM08BG == 1 && bg_get_data($@KvM08BG_id1, 0) < 7 )
		waitingroom2bg_single $@KvM08BG_id1,"bat_c08",53,128,"KvM08R_Guillaume";
	else
		donpcevent "KvM08_BG::OnReadyCheck";
	end;

OnGuillaumeQuit:
	set BG_Delay_Tick, gettimetick(2) + 1200;
	if( $@KvM08BG == 1 && bg_get_data($@KvM08BG_id1, 0) < 7 )
		waitingroom2bg_single $@KvM08BG_id1,"bat_c08",53,128,"KvM08R_Guillaume";
OnGuillaumeDie:
	if( $@KvM08BG == 2 )
	{
		set .Guillaume_Count, .Guillaume_Count - 1;
		set .Croix_Score, .Croix_Score + 1;
		bg_updatescore "bat_c08",.Guillaume_Score,.Croix_Score;
		if( .Guillaume_Count < 1 ) donpcevent "KvM08_BG::OnCroixWin";
	}
	end;

OnCroixJoin:
	if( $@KvM08BG == 1 && bg_get_data($@KvM08BG_id2, 0) < 7 )
		waitingroom2bg_single $@KvM08BG_id2,"bat_c08",146,55,"KvM08R_Croix";
	else
		donpcevent "KvM08_BG::OnReadyCheck";
	end;

OnCroixQuit:
	set BG_Delay_Tick, gettimetick(2) + 1200;
	if( $@KvM08BG == 1 && bg_get_data($@KvM08BG_id2, 0) < 7 )
		waitingroom2bg_single $@KvM08BG_id2,"bat_c08",146,55,"KvM08R_Croix";
OnCroixDie:
	if( $@KvM08BG == 2 )
	{
		set .Croix_Count, .Croix_Count - 1;
		set .Guillaume_Score, .Guillaume_Score + 1;
		bg_updatescore "bat_c08",.Guillaume_Score,.Croix_Score;
		if( .Croix_Count < 1 ) donpcevent "KvM08_BG::OnGuillaumeWin";
	}
	end;

OnReadyCheck:
	if( $@KvM08BG || $@Global_BG_Block )
		end;
	set .@Guillaume, getwaitingroomstate(0,"KvM08R_Guillaume");
	set .@Croix, getwaitingroomstate(0,"KvM08R_Croix");

	if( .@Guillaume < 7 || .@Croix < 7 )
		end;

	set $@KvM08BG, 1; // Starting
	donpcevent "KvM08R_Croix::OnEnterBG";
	donpcevent "KvM08R_Guillaume::OnEnterBG";
	donpcevent "KvM08_BG::OnStart";
	end;

OnStart:
	announce "Battleground -- A new GvG Duel has started!",0,0x696969;
	enablenpc "TherapistKvM08a";
	enablenpc "TherapistKvM08b";
	disablenpc "VintenarKvM08a";
	disablenpc "VintenarKvM08b";
	set $@KvM08BG_Victory, 0;
	sleep 2000;
	// Warp Teams
	bg_warp $@KvM08BG_id1,"bat_c08",53,128;
	bg_warp $@KvM08BG_id2,"bat_c08",146,55;
	// ScoreBoard
	set .Guillaume_Score, 0;
	set .Croix_Score, 0;
	initnpctimer;
	end;

OnTimer5000:
	areapercentheal "bat_c08",50,123,58,131,100,100;
	areapercentheal "bat_c08",141,52,149,60,100,100;
	mapannounce "bat_c08","The Battle will start in 25 seconds!!",1,0x696969;
	end;

OnTimer26000:
	mapannounce "bat_c08","The Battle will start in 4 seconds!!",1,0x696969;
	end;

OnTimer27000:
	donpcevent "::OnKvM08LightA";
	end;

OnTimer28000:
	donpcevent "::OnKvM08LightB";
	end;
	
OnTimer29000:
	donpcevent "::OnKvM08LightC";
	end;

OnTimer30000:
	// Team Members
	set .Guillaume_Count, bg_get_data($@KvM08BG_id1, 0);
	set .Croix_Count, bg_get_data($@KvM08BG_id2, 0);
	if( .Guillaume_Count < 7 || .Croix_Count < 7 )
	{
		set $@KvM08BG_Victory, 3;
		set $@KvM08BG, 3;
		mapannounce "bat_c08","There are not enough players to start the battle",1,0x696969;
		stopnpctimer;
		sleep 2000;
		donpcevent "KvM08_BG::OnStop";
		end;
	}

	set $@KvM08BG, 2; // Playing
	areapercentheal "bat_c08",50,123,58,131,100,100;
	bg_warp $@KvM08BG_id1,"bat_c08",62,119;
	areapercentheal "bat_c08",141,52,149,60,100,100;
	bg_warp $@KvM08BG_id2,"bat_c08",137,64;
	end;

OnTimer32000:
	mapannounce "bat_c08","The Battle of Kreiger Von Midgard has begun!!",1,0x696969;
	end;

OnTimer420000:
	mapannounce "bat_c08","The Battle will ends in 30 seconds!!",1,0x696969;
	end;

OnTimer450000:
	if( .Croix_Count > .Guillaume_Count )
		donpcevent "KvM08_BG::OnCroixWin";
	else if( .Croix_Count < .Guillaume_Count )
		donpcevent "KvM08_BG::OnGuillaumeWin";
	else
	{ // Draw Game
		set $@KvM08BG, 3;
		set $@KvM08BG_Victory, 3;
		bg_set_data $@KvM08BG_id1,BG_KVM,BG_TIE,1;
		bg_set_data $@KvM08BG_id2,BG_KVM,BG_TIE,1;

		stopnpctimer;
		sleep 2000;
		mapannounce "bat_c08","The time is out! This is a Tie...",1,0x696969;
		donpcevent "KvM08_BG::OnStop";
	}
	end;

OnGuillaumeWin:
	set $@KvM08BG, 3;
	set $@KvM08BG_Victory, 1;
	bg_set_data $@KvM08BG_id1,BG_KVM,BG_W0N,1;
	bg_set_data $@KvM08BG_id2,BG_KVM,BG_LOST,1;
	
	stopnpctimer;
	sleep 2000;
	mapannounce "bat_c08","The Guillaume Army has won the Battle of Kreiger Von Midgard",1,0x0000FF;
	donpcevent "KvM08_BG::OnStop";
	end;

OnCroixWin:
	set $@KvM08BG, 3;
	set $@KvM08BG_Victory, 2;
	bg_set_data $@KvM08BG_id2,BG_KVM,BG_W0N,1;
	bg_set_data $@KvM08BG_id1,BG_KVM,BG_LOST,1;

	stopnpctimer;
	sleep 2000;
	mapannounce "bat_c08","The Croix Army has won the Battle of Kreiger Von Midgard",1,0xFF0000;
	donpcevent "KvM08_BG::OnStop";
	end;

OnStop:
	disablenpc "TherapistKvM08a";
	disablenpc "TherapistKvM08b";
	enablenpc "VintenarKvM08a";
	enablenpc "VintenarKvM08b";
	// Warp Teams
	bg_warp $@KvM08BG_id1,"bat_c08",53,128;
	bg_warp $@KvM08BG_id2,"bat_c08",146,55;
	donpcevent "KvM08_BG_Out::OnBegin";
	end;

OnReset:
	stopnpctimer;
	stopnpctimer "KvM08_BG_Out";
	set .Croix_Count, 0;
	set .Guillaume_Count, 0;
	set .Croix_Score, 0;
	set .Guillaume_Score, 0;
	set $@KvM08BG_Victory, 0;
	if( $@KvM08BG_id1 ) { bg_destroy $@KvM08BG_id1; set $@KvM08BG_id1, 0; }
	if( $@KvM08BG_id2 ) { bg_destroy $@KvM08BG_id2; set $@KvM08BG_id2, 0; }
	disablenpc "TherapistKvM08a";
	disablenpc "TherapistKvM08b";
	disablenpc "VintenarKvM08a";
	disablenpc "VintenarKvM08b";
	sleep 1000;
	mapwarp "bat_c08","bat_room",155,150;
	sleep 2000;
	maprespawnguildid "bat_c08",0,3; // Just in case someone else
	bg_updatescore "bat_c08",0,0;
	sleep 2000;
	set $@KvM08BG, 0;
	donpcevent "KvM08_BG::OnReadyCheck"; // Maybe a game is ready to start
	end;
}

-	script	KvM08_BG_Out	-1,{
	end;

OnBegin:
	initnpctimer;
	end;

OnTimer3000:
	mapannounce "bat_c08","Battle of Kreiger Von Midgard will close in 1 minute!",1,0x696969;
	end;
OnTimer30000:
	mapannounce "bat_c08","Battle of Kreiger Von Midgard will close in 30 seconds!",1,0x696969;
	end;
OnTimer50000:
	mapannounce "bat_c08","Battle of Kreiger Von Midgard will close in 10 seconds!",1,0x696969;
	end;
OnTimer60000:
	donpcevent "KvM08_BG::OnReset";
	end;
}

// Battleground Therapist
// *********************************************************************

bat_c08,51,130,5	script	Therapist in battle::TherapistKvM08a	95,{
	mes "[Therapist in battle]";
	mes "Just close your eyes, and take a deep breathe.";
	mes "You can be free from pain.";
	specialeffect2 312;
	close;
}

bat_c08,148,53,1	script	Therapist in battle::TherapistKvM08b	95,{
	mes "[Therapist in battle]";
	mes "Just close your eyes, and take a deep breathe.";
	mes "You can be free from pain.";
	specialeffect2 312;
	close;
}

// Battleground rewards
// *********************************************************************

bat_c08,51,130,5	script	Guillaume Vintenar::VintenarKvM08a	419,{
	if( $@KvM08BG_Victory )
	{
		if( $@KvM08BG_Victory == 3 )
		{
			set KvM_BG_Tick, gettimetick(2) + 30;
			bg_leave;
			warp "bat_room",155,150;
			end;
		}
		else if( $@KvM08BG_Victory == Bat_Team )
		{ // Victory
			set .@Reward, 5;
			mes "[Swandery]";
			mes "Blessed Guillaume!!";
			mes "Let's enjoy our glorious victory!";
			mes "" + strcharinfo(0) + ", its a sign reflecting victory";
			close2;
		}
		else
		{ // 
			set .@Reward, 1;
			mes "[Swandery]";
			mes "You lost, but you're dedicated to this battle.";
			mes "This is a reward for your great dedication by Guillaume Marollo!";
			mes "Just take this defeat a lesson, and later you would definitely learn.";
			close2;
		}

		set KvM_BG_Tick, gettimetick(2) + 30;
		// getitem 7773, .@Reward;
		bg_leave;
		warp "bat_room",155,150;
		end;
	}
	end;
}

bat_c08,148,53,1	script	Croix Vintenar::VintenarKvM08b	415,{
	if( $@KvM08BG_Victory )
	{
		if( $@KvM08BG_Victory == 3 )
		{
			set KvM_BG_Tick, gettimetick(2) + 30;
			bg_leave;
			warp "bat_room",155,150;
			end;
		}
		else if( $@KvM08BG_Victory == Bat_Team )
		{ // Victory
			set .@Reward, 5;
			mes "[Swandery]";
			mes "Blessed Croax!!";
			mes "Let's enjoy our glorious victory!";
			mes "" + strcharinfo(0) + ", its a sign reflecting victory";
			close2;
		}
		else
		{ // 
			set .@Reward, 1;
			mes "[Swandery]";
			mes "Oh, " + strcharinfo(0) + ". Don't be sad.";
			mes "Even though we didn't win, we did our best.";
			mes "This is a Royal gift from Croix, and please don't forget this battle. We can win the next.";
			close2;
		}

		set KvM_BG_Tick, gettimetick(2) + 30;
		// getitem 7773, .@Reward;
		bg_leave;
		warp "bat_room",155,150;
		end;
	}
	end;
}

// MapFlags
// *********************************************************************

bat_c08	mapflag	battleground	2
bat_c08	mapflag	nomemo
bat_c08	mapflag	nosave	SavePoint
bat_c08	mapflag	noteleport
bat_c08	mapflag	nowarp
bat_c08	mapflag	nowarpto
bat_c08	mapflag	noreturn
bat_c08	mapflag	nobranch
bat_c08	mapflag	nopenalty
