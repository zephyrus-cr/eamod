// ==============================================================================
// BattleGround System - Flavius 1
// ==============================================================================

// Registration NPC's
// *********************************************************************

bat_room,253,74,0	script	Registration::Fl1R_Guillaume	418,{
	end;
OnInit:
	waitingroom "Battle Station 5 Players",5,"Flavius_BG1::OnGuillaumeJoin",1;
	end;
OnEnterBG:
	set $@FlaviusBG1_id1, waitingroom2bg("bat_b01",390,10,0,"Flavius_BG1::OnGuillaumeQuit","");
	end;
}

bat_room,253,97,4	script	Registration::Fl1R_Croix	414,{
	end;
OnInit:
	waitingroom "Battle Station 5 Players",5,"Flavius_BG1::OnCroixJoin",1;
	end;
OnEnterBG:
	set $@FlaviusBG1_id2, waitingroom2bg("bat_b01",10,290,1,"Flavius_BG1::OnCroixQuit","");
	end;
}

// Battleground Engine
// *********************************************************************

-	script	Flavius_BG1	-1,{
	end;

OnGuillaumeQuit:
OnCroixQuit:
	setquest 8506; // Deserter
	if( $@FlaviusBG1 != 0 )
		donpcevent "Flavius_BG1::OnDoBalance";
	end;

OnGuillaumeJoin:
OnCroixJoin:
	if( $@FlaviusBG1 == 0 )
		donpcevent "Flavius_BG1::OnReadyCheck";
	else
		donpcevent "Flavius_BG1::OnDoBalance";
	end;

OnDoBalance:
	if( $@FlaviusBG1 != 1 ) 
		end; // No Balance if if BG runing

	set .@Guillaume, bg_get_data($@FlaviusBG1_id1, 0);
	set .@Croix, bg_get_data($@FlaviusBG1_id2, 0);

	if( .@Guillaume < .@Croix )
		waitingroom2bg_single $@FlaviusBG1_id1,"bat_b01",390,10,"Fl1R_Guillaume";
	else if( .@Guillaume > .@Croix )
		waitingroom2bg_single $@FlaviusBG1_id2,"bat_b01",10,290,"Fl1R_Croix";
	else
	{
		set .@GuillaumeR, getwaitingroomstate(0,"Fl1R_Guillaume");
		set .@CroixR, getwaitingroomstate(0,"Fl1R_Croix");

		if( .@GuillaumeR <= .@CroixR )
			set .@Limit, .@GuillaumeR;
		else
			set .@Limit, .@CroixR;

		if( .@Limit + .@Guillaume > 35 )
			set .@Limit, 35 - .@Guillaume;
		if( .@Limit <= 0 ) end;

		for( set .@i, 0; .@i < .@Limit; set .@i, .@i + 1 )
		{
			waitingroom2bg_single $@FlaviusBG1_id1,"bat_b01",390,10,"Fl1R_Guillaume";
			waitingroom2bg_single $@FlaviusBG1_id2,"bat_b01",10,290,"Fl1R_Croix";
		}

		set .@Guillaume, .@Guillaume + .@Limit;
		set .@Croix, .@Croix + .@Limit;
		mapannounce "bat_room","Battleground -- Flavius [80-99] G: " + .@Guillaume + "/35, C: " + .@Croix + "/35 (Playing)",1,0x8B0000;
	}
	end;

OnReadyCheck:
	if( $@FlaviusBG1 )
		end;
	set .@Guillaume, getwaitingroomstate(0,"Fl1R_Guillaume");
	set .@Croix, getwaitingroomstate(0,"Fl1R_Croix");
	
	if( .@Guillaume < 5 || .@Croix < 5 )
	{
		if( .@Guillaume > 3 && .@Croix > 3 && !agitcheck() && $@Flavius_Flood < gettimetick(2) )
		{
			announce "Battleground -- Flavius [80-99] G: " + .@Guillaume + "/5, C: " + .@Croix + "/5",0,0x8B0000;
			set $@Flavius_Flood, gettimetick(2) + 15;
		}
		else
			mapannounce "bat_room","Battleground -- Flavius [80-99] G: " + .@Guillaume + "/5, C: " + .@Croix + "/5",1,0x8B0000;
		end;
	}

	// BG Variables
	set $@FlaviusBG1, 1;
	set .Guillaume_Score, 0;
	set .Guillaume_Loss, 0;
	set .Croix_Score, 0;
	set .Croix_Loss, 0;
	set .Match, 0;
	// Prepare NPC
	donpcevent "#gfl1_respawn::OnBGStart";
	donpcevent "#cfl1_respawn::OnBGStart";
	// Build and Warp Teams
	donpcevent "Fl1R_Guillaume::OnEnterBG";
	donpcevent "Fl1R_Croix::OnEnterBG";
	announce "Battleground -- Flavius [80-99] has started!",0,0x8B0000;
	initnpctimer;
	// Start Match!!

OnRoundStart:
	sleep 2000;
	if( $@FlaviusBG1 != 1 ) end;

	areapercentheal "bat_b01",382,2,397,17,100,100;
	areapercentheal "bat_b01",2,282,17,297,100,100;
	bg_warp $@FlaviusBG1_id1,"bat_b01",311,224;
	bg_warp $@FlaviusBG1_id2,"bat_b01",87,75;

	sleep 2000;
	if( $@FlaviusBG1 != 1 ) end;

	set .Match, .Match + 1;
	// Crystal Spawn
	set .Guillaume_Crystal, bg_monster($@FlaviusBG1_id1,"bat_b01",328,150,"Crystal Guillaume",1914,"Flavius_BG1::OnGuillaumeBreak");
	setwall "bat_b01",327,149,2,6,0,"gui_wall1-1";
	setwall "bat_b01",329,149,2,0,0,"gui_wall1-2";
	setwall "bat_b01",329,151,2,2,0,"gui_wall1-3";
	setwall "bat_b01",327,151,2,4,0,"gui_wall1-4";

	set .Croix_Crystal, bg_monster($@FlaviusBG1_id2,"bat_b01",62,150,"Crystal Croix",1915,"Flavius_BG1::OnCroixBreak");
	setwall "bat_b01",61,149,2,6,0,"cro_wall1-1";
	setwall "bat_b01",63,149,2,0,0,"cro_wall1-2";
	setwall "bat_b01",63,151,2,2,0,"cro_wall1-3";
	setwall "bat_b01",61,151,2,4,0,"cro_wall1-4";

	// Guardian Spawns
	bg_monster $@FlaviusBG1_id1,"bat_b01",328,160,"Guillaume Guardian",1949,"Flavius_BG1::OnGuiGuardian";
	bg_monster $@FlaviusBG1_id1,"bat_b01",328,140,"Guillaume Guardian",1950,"Flavius_BG1::OnGuiGuardian";
	set .Guillaume_Guardian, 2;
	bg_monster $@FlaviusBG1_id2,"bat_b01",62,160,"Croix Guardian",1949,"Flavius_BG1::OnCroGuardian";
	bg_monster $@FlaviusBG1_id2,"bat_b01",61,140,"Croix Guardian",1950,"Flavius_BG1::OnCroGuardian";
	set .Croix_Guardian, 2;
	// Announces
	mapannounce "bat_b01","The Battle of Flavius [ " + .Match + " ] Round has begun!!",1,0x8B0000;
	end;

OnRoundStop:
	// Remove Monsters
	killmonster "bat_b01","Flavius_BG1::OnGuiGuardian";
	killmonster "bat_b01","Flavius_BG1::OnCroGuardian";
	delwall "gui_wall1-1";
	delwall "gui_wall1-2";
	delwall "gui_wall1-3";
	delwall "gui_wall1-4";
	killmonster "bat_b01","Flavius_BG1::OnGuillaumeBreak";
	delwall "cro_wall1-1";
	delwall "cro_wall1-2";
	delwall "cro_wall1-3";
	delwall "cro_wall1-4";
	killmonster "bat_b01","Flavius_BG1::OnCroixBreak";
	end;

OnGuiGuardian:
	if( set(.Guillaume_Guardian, .Guillaume_Guardian - 1) <= 0 )
	{
		delwall "gui_wall1-1";
		delwall "gui_wall1-2";
		delwall "gui_wall1-3";
		delwall "gui_wall1-4";
		mapannounce "bat_b01","The Guillaume Crystal is vulnerable to attack!",1,0x0000FF;
	}
	end;

OnCroGuardian:
	if( set(.Croix_Guardian, .Croix_Guardian - 1) <= 0 )
	{
		delwall "cro_wall1-1";
		delwall "cro_wall1-2";
		delwall "cro_wall1-3";
		delwall "cro_wall1-4";
		mapannounce "bat_b01","The Croix Crystal is vulnerable to attack!",1,0xFF0000;
	}
	end;

OnGuillaumeBreak:
	donpcevent "Flavius_BG1::OnRoundStop";
	set .Guillaume_Loss, .Guillaume_Loss + 1;
	if( set(.Croix_Score, .Croix_Score + 1) < 2 )
	{
		bg_updatescore "bat_b01",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b01","The Guillaume Crystal has been destroyed!",1,0x0000FF;
		donpcevent "Flavius_BG1::OnRoundStart";
	}
	else
	{
		bg_updatescore "bat_b01",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b01","The Croix army has won the Battle of Flavius!",1,0xFF0000;
		donpcevent "Flavius_BG1::OnMatchEnd";
	}
	end;

OnCroixBreak:
	donpcevent "Flavius_BG1::OnRoundStop";
	set .Croix_Loss, .Croix_Loss + 1;
	if( set(.Guillaume_Score, .Guillaume_Score + 1) < 2 )
	{
		bg_updatescore "bat_b01",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b01","The Croix Crystal has been destroyed!",1,0xFF0000;
		donpcevent "Flavius_BG1::OnRoundStart";
	}
	else
	{
		bg_updatescore "bat_b01",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b01","The Guillaume army has won the Battle of Flavius!",1,0x0000FF;
		donpcevent "Flavius_BG1::OnMatchEnd";
	}
	end;

OnTimer600000:
	mapannounce "bat_b01","Battle of Flavius will ends in 5 minutes",1,0x8B0000;
	end;

OnTimer840000:
	mapannounce "bat_b01","Battle of Flavius will ends in 1 minute",1,0x8B0000;
	end;

OnTimer900000:
	if( .Croix_Score > .Guillaume_Score )
		mapannounce "bat_b01","The Croix army has won the Battle of Flavius!",1,0xFF0000;
	else if( .Croix_Score < .Guillaume_Score )
		mapannounce "bat_b01","The Guillaume army has won the Battle of Flavius!",1,0x0000FF;
	else
		mapannounce "bat_b01","The Battle of Flavius is over. The time is out, this is a Tie",1,0x8B0000;

OnMatchEnd:
	stopnpctimer;
	donpcevent "#gfl1_respawn::OnBGStop";
	donpcevent "#cfl1_respawn::OnBGStop";
	set $@FlaviusBG1, 2; // BG Status
	// =======================================================
	// Team Rewards
	// =======================================================
	if( .Guillaume_Score > .Croix_Score )
	{
		bg_getitem $@FlaviusBG1_id1,7829,12;
		bg_getitem $@FlaviusBG1_id2,7829,4;
		bg_getkafrapoints $@FlaviusBG1_id1,70;
		bg_getkafrapoints $@FlaviusBG1_id2,30;
	}
	else if( .Croix_Score > .Guillaume_Score )
	{
		bg_getitem $@FlaviusBG1_id1,7829,4;
		bg_getitem $@FlaviusBG1_id2,7829,12;
		bg_getkafrapoints $@FlaviusBG1_id1,30;
		bg_getkafrapoints $@FlaviusBG1_id2,70;
	}
	else
	{
		bg_getitem $@FlaviusBG1_id1,7829,4;
		bg_getitem $@FlaviusBG1_id2,7829,4;
		bg_getkafrapoints $@FlaviusBG1_id1,30;
		bg_getkafrapoints $@FlaviusBG1_id2,30;
	}
	// =======================================================
	bg_team_setquest $@FlaviusBG1_id1,8503;
	bg_team_setquest $@FlaviusBG1_id2,8503;
	sleep 2000;
	bg_warp $@FlaviusBG1_id1,"bat_b01",390,10;
	bg_warp $@FlaviusBG1_id2,"bat_b01",10,290;
	sleep 3000;
	mapannounce "bat_b01","Battle of Flavius will start 15 seconds!",1,0x8B0000;
	initnpctimer;
	end;

OnTimer10000:
	if( $@FlaviusBG1 == 2 )
		mapannounce "bat_b01","Battle of Flavius will start in 5 seconds!",1,0x8B0000;
	end;

OnTimer15000:
	if( $@FlaviusBG1 != 2 )
		end;
OnReset:
	stopnpctimer;
	donpcevent "Flavius_BG1::OnRoundStop";
	set .Guillaume_Score, 0;
	set .Guillaume_Crystal, 0;
	set .Guillaume_Loss, 0;
	set .Croix_Score, 0;
	set .Croix_Crystal, 0;
	set .Croix_Loss, 0;
	set .Match, 0;
	bg_destroy $@FlaviusBG1_id1;
	bg_destroy $@FlaviusBG1_id2;
	set $@FlaviusBG1_id1, 0;
	set $@FlaviusBG1_id2, 0;
	sleep 1000;
	mapwarp "bat_b01","bat_room",155,150;
	sleep 1000;
	maprespawnguildid "bat_b01",0,3; // Just in case someone else
	sleep 1000;
	bg_updatescore "bat_b01",0,0;
	set $@FlaviusBG1, 0;
	donpcevent "Flavius_BG1::OnReadyCheck";
	end;
}

// Battleground Therapist
// *********************************************************************

bat_b01,390,13,5	script	Therapist in battle#fl12	95,{
	switch( $@FlaviusBG1 )
	{
	case 2:
		bg_leave;
		warp "bat_room",155,150;
		break;
	case 1:
		mes "[Therapist in battle]";
		mes "Just close your eyes, and take a deep breathe.";
		mes "You can be free from pain.";
		specialeffect2 312;
		percentheal 100,100;
		repairall;
		close2;
		openstorage;
		break;
	}
	end;
}

bat_b01,10,293,5	script	Therapist in battle#fl11	95,{
	switch( $@FlaviusBG1 )
	{
	case 2:
		bg_leave;
		warp "bat_room",155,150;
		break;
	case 1:
		mes "[Therapist in battle]";
		mes "Just close your eyes, and take a deep breathe.";
		mes "You can be free from pain.";
		specialeffect2 312;
		percentheal 100,100;
		repairall;
		close2;
		openstorage;
		break;
	}
	end;
}

// Battleground Respawn
// *********************************************************************

bat_b01,390,10,0	script	#gfl1_respawn	139,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	misceffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_b01",382,2,397,17,100,100;
	areawarp "bat_b01",382,2,397,17,"bat_b01",311,224;
	initnpctimer;
	end;
}

bat_b01,10,290,0	script	#cfl1_respawn	139,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	misceffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_b01",2,282,17,297,100,100;
	areawarp "bat_b01",2,282,17,297,"bat_b01",87,75;
	initnpctimer;
	end;
}

// Flags
// *********************************************************************

bat_b01,304,231,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat1	973
bat_b01,319,231,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat2	973
bat_b01,304,218,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat3	973
bat_b01,319,218,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat4	973
bat_b01,304,231,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat5	973
bat_b01,304,231,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat6	973
bat_b01,335,142,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat7	973
bat_b01,335,157,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat8	973
bat_b01,390,16,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat9	973
bat_b01,292,163,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat19	973
bat_b01,292,136,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat20	973
bat_b01,241,185,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat21	973
bat_b01,247,179,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat22	973

bat_b01,96,81,1	duplicate(Croix camp#bat)	Croix camp#bat1	974
bat_b01,96,68,1	duplicate(Croix camp#bat)	Croix camp#bat2	974
bat_b01,79,81,1	duplicate(Croix camp#bat)	Croix camp#bat3	974
bat_b01,79,68,1	duplicate(Croix camp#bat)	Croix camp#bat4	974
bat_b01,96,81,1	duplicate(Croix camp#bat)	Croix camp#bat5	974
bat_b01,96,81,1	duplicate(Croix camp#bat)	Croix camp#bat6	974
bat_b01,59,164,1	duplicate(Croix camp#bat)	Croix camp#bat7	974
bat_b01,59,137,1	duplicate(Croix camp#bat)	Croix camp#bat8	974
bat_b01,10,296,1	duplicate(Croix camp#bat)	Croix camp#bat9	974
bat_b01,110,162,1	duplicate(Croix camp#bat)	Croix camp#bat18	974
bat_b01,110,137,1	duplicate(Croix camp#bat)	Croix camp#bat19	974
bat_b01,152,120,1	duplicate(Croix camp#bat)	Croix camp#bat20	974
bat_b01,158,114,1	duplicate(Croix camp#bat)	Croix camp#bat21	974

// MapFlags
// *********************************************************************

bat_b01	mapflag	battleground	2
bat_b01	mapflag	nomemo
bat_b01	mapflag	nosave	SavePoint
bat_b01	mapflag	noteleport
bat_b01	mapflag	nowarp
bat_b01	mapflag	nowarpto
bat_b01	mapflag	noreturn
bat_b01	mapflag	nobranch
bat_b01	mapflag	nopenalty
