// (c) 2008 - 2012 eAmod Project; Andres Garbanzo / Zephyrus
//
//  - gaiaro.staff@yahoo.com
//  - MSN andresjgm.cr@hotmail.com
//  - Skype: Zephyrus_cr
//  - Site: http://dev.terra-gaming.com
//
// This file is NOT public - you are not allowed to distribute it.
// Authorized Server List : http://dev.terra-gaming.com/index.php?/topic/72-authorized-eamod-servers/
// eAmod is a non Free, extended version of eAthena Ragnarok Private Server.

// ==============================================================================
// BattleGround System - Flavius TeamDeathMatch
// ==============================================================================

// Battleground Engine
// *********************************************************************

-	script	Flavius_TD	-1,{
	end;

OnTeam1Quit:
	setquest 8506; // Deserter
	if( $@BG_Status != 0 && getvariableofnpc(.BG_Arena$,"BG_Queue_Join") == "Flavius_TD" )
		donpcevent "BG_Queue_Join::OnJoinEvent";
OnTeam1Die:
	if( $@BG_Status == 1 && getvariableofnpc(.BG_Arena$,"BG_Queue_Join") == "Flavius_TD" )
	{
		set .Team1_Score, .Team1_Score - 1;
		donpcevent "Flavius_TD::OnValidateScore";
	}
	end;

OnTeam2Quit:
	setquest 8506; // Deserter
	if( $@BG_Status != 0 && getvariableofnpc(.BG_Arena$,"BG_Queue_Join") == "Flavius_TD" )
		donpcevent "BG_Queue_Join::OnJoinEvent";
OnTeam2Die:
	if( $@BG_Status == 1 && getvariableofnpc(.BG_Arena$,"BG_Queue_Join") == "Flavius_TD" )
	{
		set .Team2_Score, .Team2_Score - 1;
		donpcevent "Flavius_TD::OnValidateScore";
	}
	end;

OnBuildTeams:
	set $@BG_Team1,bg_team_create("bat_b03",390,10,0,"Flavius_TD::OnTeam1Quit","Flavius_TD::OnTeam1Die");
	set $@BG_Team2,bg_team_create("bat_b03",10,290,1,"Flavius_TD::OnTeam2Quit","Flavius_TD::OnTeam2Die");
	end;

OnReady:
	if( getvariableofnpc(.BG_Arena$,"BG_Queue_Join") != "Flavius_TD" )
		end;

	initnpctimer;
	set .Team1_Score, 99;
	set .Team2_Score, 99;
	bg_updatescore "bat_b03",.Team1_Score,.Team2_Score;
	sleep 2000;
	bg_warp $@BG_Team1,"bat_b03",328,150;
	bg_warp $@BG_Team2,"bat_b03",62,150;
	sleep 2000;
	bg_team_reveal $@BG_Team1;
	bg_team_reveal $@BG_Team2;
	donpcevent "#guitd_respawn::OnBGStart";
	donpcevent "#crotd_respawn::OnBGStart";
	end;

OnValidateScore:
	if( $@BG_Status != 1 || getvariableofnpc(.BG_Arena$,"BG_Queue_Join") != "Flavius_TD" )
		end;

	bg_updatescore "bat_b03",.Team1_Score,.Team2_Score;
	set .@Team1Count, bg_get_data($@BG_Team1, 0);
	set .@Team2Count, bg_get_data($@BG_Team2, 0);

	if( .Team2_Score <= 0 )
	{ // Team 1 Won
		bg_reward $@BG_Team1,7829,30,0,0,"BG_TDM",1,4,0;
		bg_reward $@BG_Team2,7829,15,0,0,"BG_TDM",1,4,2;
		mapannounce "bat_b03","The " + bg_get_data($@BG_Team1,2) + " army has won the Battle of Flavius TD!",1,bg_get_data($@BG_Team1,4);
	}
	else if( .Team1_Score <= 0 )
	{ // Team 2 Won
		bg_reward $@BG_Team1,7829,15,0,0,"BG_TDM",1,4,2;
		bg_reward $@BG_Team2,7829,30,0,0,"BG_TDM",1,4,0;
		mapannounce "bat_b03","The " + bg_get_data($@BG_Team2,2) + " army has won the Battle of Flavius TD!",1,bg_get_data($@BG_Team2,4);
	}
	else if( .@Team2Count == 0 )
	{ // All Team 2 Players quit
		set .@Rate, 99 - .Team2_Score;
		bg_reward $@BG_Team1,7829,30 * .@Rate / 99,0,0,"BG_TDM",1,4,0;
		mapannounce "bat_b03","The " + bg_get_data($@BG_Team1,2) + " army has won the Battle of Flavius TD!",1,bg_get_data($@BG_Team1,4);
	}
	else if( .@Team1Count == 0 )
	{ // All Team 1 Players quit
		set .@Rate, 99 - .Team1_Score;
		bg_reward $@BG_Team2,7829,30 * .@Rate / 99,0,0,"BG_TDM",1,4,0;
		mapannounce "bat_b03","The " + bg_get_data($@BG_Team2,2) + " army has won the Battle of Flavius TD!",1,bg_get_data($@BG_Team2,4);
	}
	else end;
	donpcevent "Flavius_TD::OnMatchEnd";
	end;

OnTimer600000:
	mapannounce "bat_b03","The Battle will ends in 5 minutes!!",1,0x808000;
	end;

OnTimer840000:
	mapannounce "bat_b03","The Battle will ends in 1 minute!!",1,0x808000;
	end;

OnTimer900000:
	if( .Team1_Score > .Team2_Score )
	{ // Team 1 Won
		bg_reward $@BG_Team1,7829,30,0,0,"BG_TDM",1,4,0;
		bg_reward $@BG_Team2,7829,15,0,0,"BG_TDM",1,4,2;
		mapannounce "bat_b03","The " + bg_get_data($@BG_Team1,2) + " army has won the Battle of Flavius TD!",1,bg_get_data($@BG_Team1,4);
	}
	else if( .Team1_Score < .Team2_Score )
	{ // Team 2 Won
		bg_reward $@BG_Team1,7829,15,0,0,"BG_TDM",1,4,2;
		bg_reward $@BG_Team2,7829,30,0,0,"BG_TDM",1,4,0;
		mapannounce "bat_b03","The " + bg_get_data($@BG_Team2,2) + " army has won the Battle of Flavius TD!",1,bg_get_data($@BG_Team2,4);
	}
	else
	{
		bg_reward $@BG_Team1,7829,15,0,0,"BG_TDM",1,4,1;
		bg_reward $@BG_Team2,7829,15,0,0,"BG_TDM",1,4,1;
		mapannounce "bat_b03","The battle is over. This is a Tie...!",1,0x808000;
	}
	donpcevent "Flavius_TD::OnMatchEnd";
	end;

OnMatchEnd:
	stopnpctimer;
	donpcevent "#guitd_respawn::OnBGStop";
	donpcevent "#crotd_respawn::OnBGStop";
	set $@BG_Status, 2;
	set .Team1_Score, 99;
	set .Team2_Score, 99;
	sleep 5000;
	donpcevent "BG_Queue_Join::OnRotate";
	bg_updatescore "bat_b03",99,99;
	end;
}

// Battleground Therapist
// *********************************************************************

bat_b03,390,13,5	script	Therapist in battle#td1	95,{
	mes "[Therapist in battle]";
	mes "Just close your eyes, and take a deep breathe.";
	mes "You can be free from pain.";
	specialeffect2 312;
	percentheal 100,100;
	repairall;
	close2;
	openstorage;
	end;
}

bat_b03,10,293,5	script	Therapist in battle#td2	95,{
	mes "[Therapist in battle]";
	mes "Just close your eyes, and take a deep breathe.";
	mes "You can be free from pain.";
	specialeffect2 312;
	percentheal 100,100;
	repairall;
	close2;
	openstorage;
	end;
}

// Battleground Respawn
// *********************************************************************

bat_b03,390,10,0	script	#guitd_respawn	139,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	misceffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_b03",382,2,397,17,100,100;
	areawarp "bat_b03",382,2,397,17,"bat_b03",306,138,327,161;
	initnpctimer;
	end;
}

bat_b03,10,290,0	script	#crotd_respawn	139,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	misceffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_b03",2,282,17,297,100,100;
	areawarp "bat_b03",2,282,17,297,"bat_b03",72,138,93,161;
	initnpctimer;
	end;
}

// Flags
// *********************************************************************

bat_b03,304,231,1	duplicate(Base Flag#bg)	Alpha Base#td_1	973
bat_b03,319,231,1	duplicate(Base Flag#bg)	Alpha Base#td_2	973
bat_b03,304,218,1	duplicate(Base Flag#bg)	Alpha Base#td_3	973
bat_b03,319,218,1	duplicate(Base Flag#bg)	Alpha Base#td_4	973
bat_b03,304,231,1	duplicate(Base Flag#bg)	Alpha Base#td_5	973
bat_b03,304,231,1	duplicate(Base Flag#bg)	Alpha Base#td_6	973
bat_b03,335,142,1	duplicate(Base Flag#bg)	Alpha Base#td_7	973
bat_b03,335,157,1	duplicate(Base Flag#bg)	Alpha Base#td_8	973
bat_b03,390,16,1	duplicate(Base Flag#bg)	Alpha Base#td_9	973
bat_b03,292,163,1	duplicate(Base Flag#bg)	Alpha Base#td_10	973
bat_b03,292,136,1	duplicate(Base Flag#bg)	Alpha Base#td_11	973
bat_b03,241,185,1	duplicate(Base Flag#bg)	Alpha Base#td_12	973
bat_b03,247,179,1	duplicate(Base Flag#bg)	Alpha Base#td_13	973

bat_b03,96,81,1	duplicate(Base Flag#bg)	Omega Base#td_1	974
bat_b03,96,68,1	duplicate(Base Flag#bg)	Omega Base#td_2	974
bat_b03,79,81,1	duplicate(Base Flag#bg)	Omega Base#td_3	974
bat_b03,79,68,1	duplicate(Base Flag#bg)	Omega Base#td_4	974
bat_b03,96,81,1	duplicate(Base Flag#bg)	Omega Base#td_5	974
bat_b03,96,81,1	duplicate(Base Flag#bg)	Omega Base#td_6	974
bat_b03,59,164,1	duplicate(Base Flag#bg)	Omega Base#td_7	974
bat_b03,59,137,1	duplicate(Base Flag#bg)	Omega Base#td_8	974
bat_b03,10,296,1	duplicate(Base Flag#bg)	Omega Base#td_9	974
bat_b03,110,162,1	duplicate(Base Flag#bg)	Omega Base#td_10	974
bat_b03,110,137,1	duplicate(Base Flag#bg)	Omega Base#td_11	974
bat_b03,152,120,1	duplicate(Base Flag#bg)	Omega Base#td_12	974
bat_b03,158,114,1	duplicate(Base Flag#bg)	Omega Base#td_13	974

// MapFlags
// *********************************************************************

bat_b03	mapflag	battleground	2
bat_b03	mapflag	nomemo
bat_b03	mapflag	nosave	SavePoint
bat_b03	mapflag	noteleport
bat_b03	mapflag	nowarp
bat_b03	mapflag	nowarpto
bat_b03	mapflag	noreturn
bat_b03	mapflag	nobranch
bat_b03	mapflag	nopenalty
