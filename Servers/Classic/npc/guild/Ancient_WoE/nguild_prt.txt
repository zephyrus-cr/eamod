//===== eAthena Script =======================================
//= War of Emperium (Prontera)
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.3
//===== Compatible With: =====================================
//= eAthena SVN
//===== Description: =========================================
//= NPCs that relate to Prontera Guild Castle 1
//===== Additional Comments: =================================
//= 1.0 First Version. No longer uses functions. [L0ne_W0lf]
//= 1.1 Corrected flag-in spawn point. [L0ne_W0lf]
//= 1.2 Corrected treasure room switch coordinates. [L0ne_W0lf]
//= 1.3 Removed dialog from interior flags. [L0ne_W0lf]
//============================================================

// Guild Flag (Outside Castle)
//============================================================
prontera,154,232,4	script	Kriemhild#np1-2	722,{
	set .@GID, GetCastleData("nguild_prt",1);
	if( .@GID == 0 )
	{
		mes " [ Edict of the Divine Rune Midgard Kingdom ]";
		mes " ";
		mes "1. Follow the ordinance of The Divine Rune Midgard Kingdom, ";
		mes "We declare that";
		mes "there is no formal master of this castle.";
		mes " ";
		mes "2. To the one who can ";
		mes "overcome all trials";
		mes "and destroy the Emperium,";
		mes "the king will endow the one with";
		mes "ownership of this castle.";
	}
	else
	{
		mes "[ Edict of the Divine Rune Midgard Kingdom ]";
		mes " ";
		mes "1. Following the ordinance of the";
		mes "Divine Rune Midgard Kingdom,";
		mes "we approve that this place is in";
		mes "the private possession of ^ff0000" + GetGuildName(.@GID) + "^000000 Guild.";
		mes " ";
		mes "2. The guild Master of ^ff0000"+ GetGuildName(.@GID) + "^000000 Guild is";
		mes "^FF0000" + GetGuildMaster(.@GID) + "^000000";
		mes "If there is anyone who objects to this,";
		mes " prove your strength and honor with a steel blade in your hand.";
	}
	if( !class2ancientwoe() )
		close;

	next;
	mes "Brave ones...";
	if( getcharid(2) == .@GID )
	{
		mes "Do you wish to return to your honorable place?";
		next;
		switch( select("Return to the Emperium Room:Return to the Castle Entrance:Quit.") )
		{
		case 1: if( getcharid(2) == GetCastleData("nguild_prt",1) )
			{
				warp "nguild_prt",96,173;
				end;
			}
			close;
		case 2: warp "nguild_prt",99,32;
			end;
		case 3:	close;
		}
	}
	else
	{
		mes "Do you wish to attack this honorable place?";
		next;
		switch( select("Return to the Castle Entrance:Quit.") )
		{
		case 1: warp "nguild_prt",99,32;
			end;
		case 2:	close;
		}
	}
	end;

OnRecvCastleNPt01:
	FlagEmblem GetCastleData("nguild_prt",1);
	end;
}

// Guild Flag (Inside Castle)
//============================================================
nguild_prt,58,56,4	script	Kriemhild#np1-4::Kriemhild4	722,{
	end;

OnRecvCastleNPt01:
	FlagEmblem GetCastleData("nguild_prt",1);
	end;
}

nguild_prt,64,56,4	duplicate(Kriemhild4)	Kriemhild#np1-7	722
nguild_prt,76,32,4	duplicate(Kriemhild4)	Kriemhild#np1-8	722
nguild_prt,84,32,4	duplicate(Kriemhild4)	Kriemhild#np1-9	722
nguild_prt,94,39,4	duplicate(Kriemhild4)	Kriemhild#np1-10	722
nguild_prt,94,24,4	duplicate(Kriemhild4)	Kriemhild#np1-11	722
nguild_prt,73,14,4	duplicate(Kriemhild4)	Kriemhild#np1-12	722
nguild_prt,73,6,4	duplicate(Kriemhild4)	Kriemhild#np1-13	722
nguild_prt,55,46,4	duplicate(Kriemhild4)	Kriemhild#np1-14	722
nguild_prt,45,46,4	duplicate(Kriemhild4)	Kriemhild#np1-15	722

// AGIT Manager
//============================================================
nguild_prt,197,197,0	duplicate(Gld_Agit_Manager)	Agit#nguild_prt	-1

// Steward
//============================================================
nguild_prt,112,181,0	duplicate(Gld_Mngr_Template)	Ahvray#nguild_prt	55

// Guardians
//============================================================
nguild_prt,112,181,0	duplicate(Gld_Guard_Template)	Guardian#nguild_prt	-1

// Kafra Staff
//============================================================
nguild_prt,96,173,0	duplicate(Gld_Kafra_Template)	Kafra Staff#nguild_prt	117

// Master's Room Exit
//============================================================
nguild_prt,15,209,0	script	#switch_NPt01	111,{
	mes " ";
	mes "There's a small lever. Will you pull it? ";
	next;
	if(select("Pull.:Do not.") == 2) close;
	warp "nguild_prt",112,183;
	end;
}

// Guild Treasure Protection
//============================================================
nguild_prt,11,208,0	duplicate(Gld_Trea_Protect)	ban_warp#nguild_prt	-1,6,6

// Guild Treasure Spawns
//============================================================
nguild_prt,11,208,0	duplicate(Gld_Trea_Spawn)	Treasure#nguild_prt	-1

// Guild Dungeon Switch
//============================================================
nguild_prt,94,200,0	duplicate(Gld_Dun_Template)	#nguild_prt	111
