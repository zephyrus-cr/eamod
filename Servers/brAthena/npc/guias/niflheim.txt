//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: guia_niflheim.txt                      |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 1.0/1.1/1.2                         |
//   |--------------------------------------------------------|
//   | Descrição: Guias da Cidade de Niflheim.                |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Megasantos]                      |
//   | 1.1 Otimização [Holy & Hold]                           |
//   | 1.2 Tradução PT-BR [Protimus]                          |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

niflheim,201,187,3	script#re	Niflheim Guide#01nif	849,{
	mes "[Niflheim Guide]";
	mes "Welcome to.. ^8B4513Niflheim^000000...";
	mes "the Realm of the Dead...";
	mes "Do you need help navigating the realm...?";
	while (1) {
		next;
		switch(select("[ Tool Shop ]:[ Pub ]:[ Weapon Shop ]:[ Witch's Castle ]:Remove Marks from Mini-Map:Cancel")) {
		case 1:
			callsub L_Mark,"^0000FFTool Shop";
			viewpoint 1,218,196,0,0x0A82FF;
			break;
		case 2:
			callsub L_Mark,"^006400Pub";
			viewpoint 1,189,208,1,0xAAFF00;
			break;
		case 3:
			callsub L_Mark,"^800080Weapon Shop";
			viewpoint 1,218,170,2,0xDA70D6;
			break;
		case 4:
			callsub L_Mark,"^FF1493Witch's Castle";
			viewpoint 1,255,195,3,0xFF1493;
			break;
		case 5:
			mes "[Niflheim Guide]";
			mes "Sure, I'll remove all marks from your mini-map...";
			mes "Is there anything else I can do for you...?";
			viewpoint 2,1,1,0,0xFFFFFF;
			viewpoint 2,1,1,1,0xFFFFFF;
			viewpoint 2,1,1,2,0xFFFFFF;
			viewpoint 2,1,1,3,0xFFFFFF;
			break;
		case 6:
			mes "[Niflheim Guide]";
			mes "......Bye~!..";
			close;
		}
	}
	end;
L_Mark:
	mes "[Niflheim Guide]";
	mes "Let me mark the location of the";
	mes getarg(0)+"^000000...";
	mes "on your mini-map.";
	mes "Would you like to check any other locations?";
	return;
}

niflheim,107,156,6	script#pre	Homem Nervoso#nif	798,{
	mes "[Lee karl]";
	mes "Por que você veio a um lugar tão horrível como este? Acho que você acabou se perdendo, isso sim.";
	mes "Eu fui atraído pelas altas árvores deste lugar. Demorou um bom tempo até eu perceber que eu estava perdido. E que não havia mais volta.";
	next;
	mes "[Lee karl]";
	mes "Eu já procurei pela saída em todos os lugares. Por isso, estou bem acostumado com o lugar.";
	mes "Lembrar das localidades por aqui pode significar a diferença entre a vida e a morte.";
	next;
	switch(select("Perguntar sobre a cidade.:Limpar indicações do minimapa.:Cancelar.")) {
	case 1:
		mes "[Lee Karl]";
		mes "Então, onde você quer ir?";
		next;
		switch(select("Castelo da Feiticeira.:Loja de Útilidades.:Loja de Armas.:Hotel.:Cancelar.")) {
		case 1:
			mes "[Lee karl]";
			mes "Uma feiticeira medonha vive em Nifflheim.";
			mes "^FF3355+^000000O castelo dela é logo ali.";
			mes "Se você sentir um calafrio na espinha, saia correndo!";
			viewpoint 1,253,191,2,0xFF3355;
			break;
		case 2:
			mes "[Lee Karl]";
			mes "A loja de utilidades é bem ali ^CE6300+^000000.";
			mes "Existem alguns itens que você não pode comprar no mundo dos vivos. Confira.";
			Emotion e_wah;
			viewpoint 1,217,196,3,0xCE6300;
			break;
		case 3:
			mes "[Lee karl]";
			mes "A loja de armas é por ali ^55FF33+^000000.";
			mes "Existem algumas armas que você não pode comprar no mundo dos vivos. Confira.";
			Emotion e_wah;
			viewpoint 1,216,171,4,0x55FF33;
			break;
		case 4:
			mes "[Lee Karl]";
			mes "O hotel fica ali ^3355FF+^000000,";
			mes "As pessoas mortas costumam fazer festas por lá. Mexa o esqueleto!";
			viewpoint 1,189,207,5,0x3355FF;
			break;
		case 5:
			mes "[Lee Karl]";
			mes "Se você quiser apagar todas as marcações no seu mapa, selecione a opção 'Limpar indicações do minimapa'";
		}
		break;
	case 2:
		viewpoint 2,253,191,2,0xFF3355;
		viewpoint 2,217,196,3,0xCE6300;
		viewpoint 2,216,171,4,0x55FF33;
		viewpoint 2,189,207,5,0x3355FF;
		mes "[Lee Karl]";
		mes "Apaguei todas as marcações do seu mapa.";
		mes "Volte se quiser que eu indique as direções novamente.";
		break;
	case 3:
		mes "[Lee karl]";
		mes "Tome cuidado, algumas coisas parecem mortas mas estão bem vivas...";
		mes "Boa sorte!";
		break;
	}
	close;
}