//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: kunais.txt                             |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 1.3/1.3a/1.2                        |
//   |--------------------------------------------------------|
//   | Descrição: Trocador de kunais + shurikens.             |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Shiraz]                          |
//   | 1.1 Tradução PT-BR [insidemybrain]                     |
//   | 1.2 Otimização [Mark & Clare]                          |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

que_ng,72,29,3	script	Mercador de Kunais	83,{
	if(BaseClass == Job_Ninja) {
		mes "[Kashin]";
		mes "Sou Kashin, distribuidor de Kunais para Ninjas.";
		mes "Dê uma olhada e me diga se você está interessado em alguns dos itens que tenho.";
		next;
		switch(select("10 Kunais - Veneno Mortal:10 Kunais - Nevasca:10 Kunais - Sopro do Vento:10 Kunais - Terra Negra:10 Kunais - Onda de Calor:Cancelar")) {
		case 1: callsub Kunai_Trade,13250,20,7524,1,13259; break;
		case 2: callsub Kunai_Trade,13251,8,7522,2,13255; break;
		case 3: callsub Kunai_Trade,13252,4,7523,2,13257; break;
		case 4: callsub Kunai_Trade,13253,2,7524,1,13256; break;
		case 5: callsub Kunai_Trade,13254,1,7521,2,13258; break;
		case 6:
			mes "[Kashin]";
			mes "Bem, obrigado então por visitar a minha loja.";
			mes "Sinta-se a vontade para voltar caso queira algumas Kunais.";
			mes "Adeus por enquanto~";
			close;
		}
	}
	mes "[Kashin]";
	mes "Sou Kashin, distribuidor de Kunais para Ninjas.";
	mes "Se você tem alguns amigos que sejam Ninjas, conte para eles sobre mim.";
	close;

Kunai_trade:
	mes "[Kashin]";
	mes "Você pode trocar:";
	mes ""+getarg(1)+" "+getitemname(getarg(0))+" and";
	mes ""+getarg(3)+" "+getitemname(getarg(2))+" for every";
	mes "set of 10 "+getitemname(getarg(4))+".";
	next;
	mes "[Kashin]";
	mes "Eu só posso lhe dar 500 pacotes de Kunai por vez.";
	mes "Caso você queira cancelar a negociação digite o valor ''0''. Quantos";
	mes "pacotes de Kunai você gostaria?";
	next;
	input .@amount;
	mes "[Kashin]";
	if(.@amount < 1 || .@amount > 500) {
		mes "Hã? Me desculpe eu não posso lhe dar esse número de pacotes de Kunais.";
		mes "Por favor, escreva um valor menor que 500.";
		close;
	}
	if(countitem(getarg(0)) < .@amount*getarg(1) || countitem(getarg(2)) < .@amount*getarg(3)) {
		mes "Hmm, você não tem";
		mes "a quantidade de item certa para esse pacote de Kunai.";
		mes "Por favor reveja seus itens e volte.";
		close;
 	}
	if(checkweight(getarg(4), .@amount*10) == 0) {
		mes "Hmm, parece que você não tem espaço no seu inventório.";
		mes "Arrume algum espaço nele e volte para que podemos finalizar a nossa negociação.";
		close;
	}
	mes "Ótimo, está tudo em ordem, agora só é preciso completar a negociação.";
	mes "Tenho certeza que você ficará muito satisfeito com essas Kunais.";
	delitem getarg(0),getarg(1)*.@amount;
	delitem getarg(2),getarg(3)*.@amount;
	getitem getarg(4),10*.@amount;
	close;
}	