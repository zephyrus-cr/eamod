//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: Sorveteiro                             |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 1.4/1.4/1.2                         |
//   |--------------------------------------------------------|
//   | Descrição: Vendedor de sorvetes.                       |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Shiraz]                          |
//   | 1.1 Tradução PT-BR [insidemybrain]                     |
//   | 1.2 Otimização [Mark & Clare]                          |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

alberta,120,45,2	script	Sorveteiro#alb	85,{
	mes "[Sorveteiro]";
	mes "Um sorvete fresco feito com a neve de Lutie!";
	mes "Aproveite, ele não esterá à venda por muito tempo!";
	mes "^3355FF100 Zeny^000000 por Sorvete!";
	next;
	if(select("Me dê sorvete!:Cancelar") == 2) {
		mes "[Sorveteiro]";
		mes "Você tem certeza que não quer nada?";
		mes "Não estarei vendendo por muito tempo, e quando acabar de vender, não voltarei mais!!!";
		close;
	}
	mes "[Sorveteiro]";
	mes "Um sorvete fresco feito com a neve de Lutie!";
	mes "Todos querem meus deliciosos sorvetes, mas nós temos uma certa quantia, então você só poderá comprar 5 por vez!!";
	next;
		input .@input;
		if(.@input < 1) {
			mes "[Sorveteiro]";
			mes "Já que você não quer comprar nada, por favor deixe eu atender o próximo cliente e deixá-lo fazer as compras?";
			mes "Obrigado.";
			close;
		}
		if(.@input > 5) {
			mes "[Sorveteiro]";
			mes "Eita!";
			mes "Você quer sorvetes demais.";
			mes "Querido cliente, se você comer mais do que 5 Sorvetes, terá de ir ao banheiro várias vezes à noite....";
			close;
	}
	if(Zeny<.@input*100) {
		mes "[Sorveteiro]";
		mes "Querido cliente, sua carteira é a luz.";
		mes "O preço é ^3355FF100 Zeny^000000 por Sorvete.";
		close;
	}
	if(checkweight(536,.@input) == 0) {
		mes "[Sorveteiro]";
		mes "Querido cliente, você está carregando coisas demais.";
		mes "Sorvete é ótimo, mas primeiro verifique o que está carregando para depois comprá-lo.";
		close;
	}
	set Zeny,Zeny-100*.@input;
	getitem 536,.@input;	// Sorvete
	close;
}

// Campo de Morroc
moc_fild16,88,304,4	duplicate(Sorveteiro#alb)	Sorveteiro#moc1	85

// Morroc
//morocc,160,144,4	duplicate(Sorveteiro#alb)	Sorveteiro#moc2	85