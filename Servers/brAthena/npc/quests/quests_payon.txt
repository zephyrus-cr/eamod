//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: Quests Payon                           |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 1.0/1.7/1.1                         |
//   |--------------------------------------------------------|
//   | Descrição: Quests dos itens para cabeça 'Faca Falsa',  |
//   | 'Aquecedor de Orelha', 'Máscara de Oxigênio', 'Chapéu  |
//   | de Deviruchi' e 'Elmo Angelical'.                      |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Cabeçalho Criado [Holy]                            |
//   | 1.1 Tradução PT-BR [Barsaglini]                        |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

// Quest Faca Falsa
//=============================================
payon_in01,99,72,4	script	Vovó	103,{
	if (checkweight(908,1) == 0) {
		mes "^3355FFEspere um segundo! Agora, você está carregando muitos itens com você. Por favor, volte depois de colocar algumas de suas coisas no Armazém da Kafra.^000000";
		close;
	}
	if (countitem(1049) > 3) {
		mes "[Vovó]";
		mes "Eu gostaria de poder fazer algumas roupas para meus netos para a temporada de festivais...";
		next;
		mes "[Vovó]";
		mes "Oh! Você pode me dar a sua ^3355FFSaia Virginal^000000? Eu preciso de quatro delas. Por favor, eu estou te implorando.";
		next;
		switch(select("Certo.:De forma alguma!")) {
		case 1:
			delitem 1049,4; // Saia Virginal
			mes "[Vovó]";
			mes "Oh! Muito obrigado! Agora eu posso fazer algumas roupas para minha neta.";
			next;
			mes "[Vovó]";
			mes "Ah, Espere! Eu quase me esqueci. Eu era uma atriz bastante popular na minha juventude. Meus amigos atores me deram um presente de casamento quando eu deixei a turma. Eu guardei isso para me lembrar desses momentos preciosos.";
			next;
			mes "[Vovó]";
			mes "Se estiver de acordo...";
			mes "Eu gostaria de lhe dar isto.";
			next;
			getitem 2293,1; // Faca Falsa
			mes "[Vovó]";
			mes "Adeus, aventureiro!";
			mes "Obrigado por fazer uma velha tão feliz...";
			close;

		case 2:
			mes "[Vovó]";
			mes "*Suspiro* Como poderia conseguir uma ^3355FFSaia Virginal^000000 sozinha? Eu estou tão fraca e frágil, até me balançar nessa cadeira de balanço me cansa. *Choro* Eu só quero fazer minha neta feliz...";
			close;
		}
	}
	mes "[Vovó]";
	mes "Eu pego cogumelos nas montanhas de Payon todos os dias. O tempo passa e antes de eu perceber, já chegou a temporada do festival. Eu gostaria de poder fazer roupas para toda a minha família este ano.";
	next;
	mes "[Vovó]";
	mes "...";
	mes "Mas para isso, eu preciso de ^3355FF4 Saias Virginais^000000...";
	next;
	mes "[Vovó]";
	mes "Jovem aventureiro, por favor, me ajude. Se você achar alguma ^3355FFSaia Virginal^000000, por favor, traga-as para mim. Eu ficaria muito grata...";
	close;
}

// Quest Aquecedor de Orelha
//=============================================
payon_in01,18,10,4	script	Senhora Mística	76,{
	mes "[Senhora Mística]";
	mes "Minha família tem produzido e vendido um produto especial de inverno chamado ^3355FFAquecedor de Orelha^000000 por muitos anos. Nós nos mudamos para cá, mas o clima é sempre quente, então não poderemos ganhar a vida.";
	next;
	mes "[Senhora Mística]";
	mes "Se você planeja viajar para regiões mais frias, é melhor você levar alguns ^3355FFAquecedores de Orelha^000000...";
	emotion e_dots;
	next;
	mes "[Senhora Mística]";
	mes "Aquecedores de Orelha é a especialidade da minha família, e nós o fornecemos aos clientes que tenham ^FF33551 Rubi Amaldiçoado^000000, ^3311AA1 Fone de Ouvido, ^000000 ^3355FF 200 Plumas^000000 e ^DDDD005.000 zenys^000000.";
	emotion e_dots;
	next;
	switch(select("Ah é? Isso me parece bom!:Não, obrigado, senhora.")) {
	case 1:
		if ((countitem(724) > 0) && (countitem(949) > 199) && (countitem(5001) > 0) && (Zeny > 4999)) {
			mes "[Senhora Mística]";
			delitem 724,1;  // Rubi Amaldiçoado
			delitem 949,200;  // Pluma
			delitem 5001,1;  // Fone de Ouvido
			set Zeny,Zeny-5000;
			mes "Aqui, vou lhe dar este que já foi feito.";
			getitem 2283,1; // Aquecedor de Orelha
			next;
			mes "[Senhora Mística]";
			mes "Obrigado por adquirir nosso produto. Você não vai se arrepender de comprar o nosso Aquecedor de Orelha! Satisfação garantida!";
			emotion e_dots;
			close;
		}
		mes "[Senhora Mística]";
		mes "Oh querido...";
		mes "Você não tem dinheiro suficiente ou itens. Infelizmente, não podemos dar descontos. Por favor, entenda que nós temos que ganhar a vida...";
		emotion e_dots;
		close;

	case 2:
		mes "[Senhora Mística]";
		mes "Isso é bom.";
		mes "Eu acredito que nos";
		mes "encontraremos novamente.";
		mes "...Um destes dias.";
		emotion e_dots;
		close;
	}
}

// Quest Máscara de Oxigênio
//=============================================
payon_in02,25,71,4	script	Garoto	86,{
	if (countitem(701) > 4) {
		mes "[Jovem]";
		mes "Arrrgggghh!";
		mes "Ah, não, eu não posso...";
		next;
		mes "[Jovem]";
		mes "*Huk*";
		mes "Agora, até meus olhos ficaram loucos!! Por que esses insetos, de repente, parecem tão bonitos?! Nãooooooo!!!";
		next;
		switch(select("Mostrar Armadilhas para Insetos.:Entregar Armadilhas para Insetos.:Cancelar.")) {
		case 1:
			mes "[Jovem]";
			mes "Oooooohhhh!!!";
			mes "Isso, isso é";
			mes "uma?!";
			mes "Uma...";
			mes "Armadilha para Insetos!!";
			next;
			mes "[Jovem]";
			mes "Eu vou ser breve! Me dê ela, e eu te darei o meu tesouro! O que acha da minha idéia? Quer negociar?";
			close;

		case 2:
			if (countitem(701) > 4) {
				delitem 701,5;  // Armadilha para Insetos
			}
			mes "[Jovem]";
			mes "Muhahahahah!!!";
			mes "Eu finalmente tenho isso!";
			mes "Armadilha para Insetos!";
			mes "Estúpidos e nojentos";
			mes "Besouros-Ladrões!";
			mes "Vocês vão me pagar!!!";
			next;
			mes "[Jovem]";
			mes "Huk...Huk...";
			mes "Desculpe, acabei perdendo o meu controle por um momento. Agora vou dar o meu tesouro, como prometido.";
			next;
			getitem 5004,1; // Máscara de Oxigênio
			mes "[Homem Jovem]";
			mes "Quando você usar isso em sua boca e começar a respirar, ela vai filtrar as impuresas antes de chegar aos seus pulmões.";
			next;
			mes "[Jovem]";
			mes "Hahahahahaha!";
			mes "Mal posso esperar";
			mes "para usar essa";
			mes "Armadilha para Insetos!";
			close;

		case 3:
			mes "[Jovem]";
			mes "Huhuhuhuhu...";
			mes "E se eu não conseguir me livrar desses Besouros-Ladrões? Talvez eu cresça e passe a amá-los... *Huk*";
			close;
		}
	}
	mes "[Jovem]";
	mes "Wahhhhhh! Eu, eu...";
	mes "Eu não aguento mais!";
	mes "Seu besouro fedido, desgraçado!";
	next;
	switch(select("Continuar.:Cancelar.")) {
	case 1:
		mes "[Jovem]";
		mes "Oh cara...";
		mes "Tudo começou quando meus pais faleceram quando eu era um garotinho. No início eu tinha que trabalhar para viver, e teve um momento muito difícil sabe...";
		next;
		mes "[Jovem]";
		mes "Eu trabalhei por 10 anos na esperança de comprar a minha casa dos sonhos, onde eu poderia encontrar alguma sensação de paz e conforto novamente. Eventualmente, eu pensei que tinha zenys para pagar o meu próprio lar doce lar.";
		next;
		mes "[Jovem]";
		mes "Infelizmente eu não tenho dinheiro suficiente para comprar a casa agradável que eu estava de olho. Mas então, eu encontrei esta casa, que era maior e mais barata do que a primeira. Comprei sem pensar...";
		next;
		mes "[Jovem]";
		mes "Doce Jesus! Acontece que, é um paraíso para Besouros-Ladrões!!!";
		next;
		mes "[Jovem]";
		mes "Eu tentei fazer tudo o que eu podia fazer. Eu já tentei espantá-los, matá-los, mas isso só me trouxe paz por um momento! Eu já tentei até queimar e trazer essa casa abaixo!";
		next;
		mes "[Jovem]";
		mes "*Suspiro* Quer dizer, que realmente, eu preciso da ^3355FF Armadilha para Insetos^000000 para se livrar deles? As pessoas dizem que raramente elas são encontradas em lugares distantes.";
		next;
		mes "[Jovem]";
		mes "Se eu pudesse comprar 5 delas, eu não ficaria mais frustrado com esses insetos nojentos, nunca mais...";
		close;

	case 2:
		mes "[Jovem]";
		mes "Essa luta sem fim contra os insetos me deixam exausto. Eu desisto! Façam o quiserem seu canalhas!";
		close;
	}
}

// Quests Chapéu de Deviruchi / Elmo Angelical
//=============================================
payon_in01,56,12,7	script	Jovem#12	89,{
	mes "[Jovem]";
	mes "...O que é isso?";
	next;
	switch(select("Você pode me fazer um item especial?:Ei.")) {
	case 1:
		mes "[Jovem]";
		mes "Huh? Então você já sabe no que sou especializado? Não vejo necessidade de negar. Me diga o que você quer.";
		next;
		switch(select("Elmo Angelical.:Chapéu de Deviruchi.:Eu volto depois.")) {
		case 1:
			mes "[Jovem]";
			mes "1 Elmo [1].";
			mes "1 Asa de Anjo.";
			mes "5 Presas de Hatii.";
			mes "Isso é tudo que eu preciso.";
			next;
			mes "[Jovem]";
			mes "Aguarde! Só para você saber, eu não me importo se os itens no seu inventário foram melhorados ou contém cartas.";
			next;
			mes "[Jovem]";
			mes "Eu quero dizer, qualquer item melhorado ou com carta será perdido quando eu fizer este item. Portanto, tenha cuidado quando você entregar o material para mim.";
			next;
			mes "[Jovem]";
			mes "Você reuniu os itens? Em seguida, entregue-os então.";
			next;
			switch(select("Entregar os itens.:Não entregar os itens.")) {
			case 1:
				if ((countitem(2229) > 0) && (countitem(2254) > 0) && (countitem(7036) > 4)) {
					mes "[Jovem]";
					mes ".....Hm.";
					mes "Muito bem, aqui está o seu Elmo Angelical. Leve-o.";
					delitem 2229,1;  // Elmo
					delitem 2254,1;  // Asas de Anjo
					delitem 7036,5;  // Presa de Hati
					close2;
					getitem 5025,1; // Elmo Angelical
					end;
				}
				mes "[Jovem]";
				mes "...O número de itens não está certo. É melhor checar.";
				close;

			case 2:
				mes "[Jovem]";
				mes "Faça como quiser...";
				close;
			}

		case 2:
			mes "[Jovem]";
			mes "600 Pequenos Chifres Malignos.";
			mes "40 Garras de Grifo.";
			mes "Isso é tudo que eu preciso.";
			next;
			mes "[Jovem]";
			mes "Aguarde! Só para você saber, eu não me importo se os itens no seu inventário foram melhorados ou contém cartas.";
			next;
			mes "[Jovem]";
			mes "Eu quero dizer, qualquer item melhorado ou com carta será perdido quando eu fizer este item. Portanto, tenha cuidado quando você entregar o material para mim.";
			next;
			mes "[Jovem]";
			mes "Você reuniu os itens? Em seguida, entregue-os então.";
			next;
			switch(select("Entregar os itens.:Não entregar os itens.")) {
			case 1:
				if ((countitem(1038) > 599) && (countitem(7048) > 39)) {
					mes "[Jovem]";
					mes ".....Hm. Aqui está o seu Chapéu de Deviruchi. Por favor, pegue-o.";
					delitem 1038,600;// Pequeno Chifre Maligno
					delitem 7048,40; // Garra de Grifo
					close2;
					getitem 5038,1;  // Chapéu de Deviruchi
					end;
				}
				mes "[Jovem]";
				mes "...O número de itens não está certo. É melhor checar.";
				close;

			case 2:
				mes "[Jovem]";
				mes "Faça como quiser...";
				close;
			}

		case 3:
			mes "[Jovem]";
			mes "Hm. Tudo bem.";
			mes "Então nos vemos depois.";
			close;
		}

	case 2:
		mes "[Jovem]";
		mes "Hm.";
		mes "Se cuide.";
		close;
	}
}