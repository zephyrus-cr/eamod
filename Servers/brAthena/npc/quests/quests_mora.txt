//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |	
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: guardiao_perdido.txt                   |	
//   |--------------------------------------------------------|
//   | Criado por: brA Dev. Team                              |
//   |--------------------------------------------------------|
//   | Versões brA: 1.1                                       |
//   |--------------------------------------------------------|
//   | Descrição: Quest do Guardião Perdido.                  |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Hold]                            |
//   | 1.1 Criado Script [Protimus & Hold]                    |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

1@mist	mapflag	loadevent
1@mist,97,31,3	script	Jovem Misterioso#npc_mist3	512,{
	if (loki == 0) {
		mes "[Jovem Misterioso]";
		mes "Você ... Você veio do acampamento que fica ao leste da cidade?";
		next;
		mes "[Jovem Misterioso]";
		mes "Mas, pelo amor de Deus, o que aconteceu neste lugar?";
		mes "Parece que há uma abertura vindo de Bifrost.";
		next;
		mes "[Jovem Misterioso]";
		mes "Não subestime este lugar, você vai precisar de ajuda.";
		next;
		select("Quem é você?");
		mes "[Loki]";
		mes "Mercenário da Guilda. Loki";
		mes "Este é meu nome, e qual o seu?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "....";
		mes "Meu nome é "+strcharinfo(0)+"....";
		next;
		mes "[Loki]";
		mes "Ok. "+strcharinfo(0)+"";
		mes "Você está pronto para explorar a floresta?";
		next;
		if(select("Sim.","Não.") == 1) {
			mes "[Loki]";
			mes "Parece que você está bastante determinado... Você viu uma garota de cabelo roxo?";
			next;
			if(select("Nunca.","Você viu...?" ) == 1) {
				mes "[Loki]";
				mes "Ok";
				mes "Eu não consigo detectar a presença dela.";
				next;
				mes "[Loki]";
				mes "Então, você pode me ajudar?";
				mes "Se der sorte, ainda poderemos nos encontrar no vilarejo de Mora. Claro, depois que você cruzar a floresta.";
				setquest 7212;
				set loki,1;
				close2;
				instance_announce 0, "Sussurro: Quem...? Quem está aí?",bc_map,"0x008000";
				end;
			}
		}
	} else if(loki == 1) {
		mes "[Loki]";
		mes "Você precisa completar o calabouço para continuar sua aventura.";
	} else if(loki == 2) {
		mes "[Loki]";
		mes "Vejo você em Mora. Se resolver ir lá algum dia.";
	} else {
		mes "[Jovem Misterioso]";
		mes "...";
	}
	close;
	
OnPCLoadMapEvent:
	if (strcharinfo(3) == strnpcinfo(4) && loki < 2)
		showevent 1,0;
	end;
}

1@mist,184,304,3	script	Loki#npc_mist2	512,{
	if (loki == 1) {
		mes "[Loki]";
		mes "...........?";
		mes "..........";
		mes "??? ??? ??????.";
		next;
		if(select("O que está acontecendo?","A garota..?" ) == 1) {
			mes "[Loki]";
			mes "Esta é Niddhog, a Guardiã de Yggdrasil.";
			mes "Era ela que eu estava procurando.";
			mes "Agora ela não vai mais ficar andando por";
			mes "aí enlouquecida de um lado para outro.";
			next;
			mes "[Loki]";
			mes "........";
			next;
			select("E agora?" );
			mes "[Loki]";
			mes "Eu vou esperar um amigo que vai me trazer algumas poções. Obrigado por sua ajuda.";
			next;
			mes "[Loki]";
			mes "Vejo você em Mora. Se resolver ir lá algum dia.";
			completequest 7212;
			setquest 7213;
			set loki,2;
			if(getcharid(0) == getpartyleader(getcharid(1),2))
				set wanderingsguardians,3;
			close;
		}
	} else if(loki == 2) {
		mes "[Loki]";
		mes "Vejo você em Mora. Se resolver ir lá algum dia.";
	} else {
		mes "[Jovem Misterioso]";
		mes "...";
	}
	close;

OnPCLoadMapEvent:
	if (strcharinfo(3) == strnpcinfo(4) && loki == 1)
		showevent 1,0;
	end;
}

1@mist,182,303,3	script	Garota Desmaiada#npc_mist1	511,{
	mes "Uma garota desmaiada no chão...";
	close;
}

mora,60,148,4	script	Loki#mora	512,{
	if(wanderingsguardians > 4) {
		mes "[Loki]";
		mes "Você trouxe de volta paz para nosos vilarejo. Nosso povo agora reconhece você como sendo um aventureiro experiente.";
		mes "Você pode encontrar artefatos interessantes em Mora. Eles podem ser comprados com a moeda local, as Moedas de Mora. Você pode conseguí-las conversando com os moradores.";
	} else if(wanderingsguardians == 4) {
		mes "[Loki]";
		mes "Eu estava esperando por você...";
		next;
		mes "[Loki]";
		mes "Estou te devendo uma recompensa pelo que você fez!";
		next;
		getitem 2568,1;
		getitem 2858,1;
		getexp 400000,400000;
		completequest 7213;
		mes "[Loki]";
		mes "Aqui está sua recompensa. Você agora é um morador de Mora. O povo de Mora está agradecido pelo que fez. Obrigado mil vezes...";
		set wanderingsguardians,5;
	} else {
		mes "[Loki]";
		mes "...";
	}
	close;

OnPCLoadMapEvent:
	if (strcharinfo(3) == "mora" && wanderingsguardians == 4) 
		showevent 1,0;
	end;	
}

mora,56,149,4	script	Garota Coberta de Flores#mora	510,{
	if(wanderingsguardians > 3) {
		mes "[Garota Coberta de Flores]";
		mes "O povo de Mora está aliviado agora que o Dragão não está mais na floresta. Você é reconhecido como herói pelo vilarejo...";
	} else if(Loki == 2) {
		mes "[Garota Coberta de Flores]";
		mes "Olá, Eu faço parte da guarda real de Mora. Nosso vilarejo está passando por várias dificuldades por causa do Dragão Púrpura...";
		next;
		mes "[Garota Coberta de Flores]";
		mes "Muito obrigada, nosso vilarejo agora está em paz. Você foi incrível para um forasteiro... Você é bem-vindo caso queira morar em Mora.";
		next;
		mes "[Garota Coberta de Flores]";
		mes "Você merece uma recompensa pelo que fez. Peça para Loki. Acho que ele tem algo especial guardado para você...";
		set wanderingsguardians,4;
	} else {
		mes "[Garota Coberta de Flores]";
		mes "...";
	}
	close;

OnPCLoadMapEvent:
	if (strcharinfo(3) == "mora" && Loki == 2 && wanderingsguardians < 4)
		showevent 1,0;
	end;
}