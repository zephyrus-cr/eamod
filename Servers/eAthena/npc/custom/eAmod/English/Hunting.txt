// Daily Hunting Missions
// ==============================================================================

prontera,178,188,1	script	Looking for a job?::MissionSign	837,{
	mes "^0000FF* Looking for a job?";
	mes "* Has the skill to hunt creatures?";
	mes "* Wanna earn items and money?^000000";
	mes "...";
	mes "Find Zeph in this House and don't waste more time!";
	close;
}

function	script	MissionInfo	{
	set .@Mission_Lvl, 0;
	set .@Mission_Exp, 0;
	set .@Mission_Job, 0;
	
	mes "[^FFA500Mission Contracts^000000]";
	for( set .@i, 1; .@i < 6; set .@i, .@i + 1 )
	{
		set .@MobID, getd("Mission_ID" + .@i);
		mes "^0000FF" + getd("Mission_Count" + .@i) + "^000000 x ^FFA500" + strmobinfo(2,.@MobID) + "^000000 (ID : " + .@MobID + ")";

		set .@Mission_Lvl, .@Mission_Lvl + strmobinfo(3, .@MobID);
		set .@Mission_Exp, .@Mission_Exp + (strmobinfo(6, .@MobID) * 10);
		set .@Mission_Job, .@Mission_Job + (strmobinfo(7, .@MobID) * 10);
	}

	set .@Mission_Zeny, .@Mission_Lvl * 150;
	next;

	mes "[^FFA500Mission Details^000000]";
	mes "Level : ^0000FF" + .@Mission_Lvl + "^000000";

	if( .@Mission_Lvl < 125 )
	{
		mes "Difficulty : ^008000Low^000000";
		set .@Mission_Points, 5;
	}
	else if( .@Mission_Lvl < 250 )
	{
		mes "Difficulty : ^000080Medium^000000";
		set .@Mission_Points, 10;
	}
	else
	{
		mes "Difficulty : ^FF0000High000000";
		set .@Mission_Points, 15;
	}
	
	set .@Time_Left, Mission_Tick - gettimetick(2);
	if( .@Time_Left <= 0 )
		mes "Time Remaining : ^FF0000Expired^000000";
	else if( .@Time_Left > 3600 )
		mes "Time Remaining : ^0000FF" + (.@Time_Left / 3600) + " hour(s)^000000.";
	else if( .@Time_Left > 60 )
		mes "Time Remaining : ^0000FF" + (.@Time_Left / 60) + " minute(s)^000000.";
	else
		mes "Time Remaining : ^0000FF" + (.@Time_Left) + " second(s)^000000.";
	next;

	mes "[^FFA500Mission Prizes^000000]";
	mes "Base Exp : ^0000FF" + .@Mission_Exp + "^000000";
	mes "Job Exp : ^0000FF" + .@Mission_Job + "^000000";
	mes "Zeny : ^0000FF" + .@Mission_Zeny + "^000000";
	mes "Points : ^0000FF" + .@Mission_Points + "^000000";
	mes "Cash Points : ^0000FF75^000000";
	return;
}

prt_in,94,73,0	script	Contracts	111,{
	if( Mission_Tick <= 0 ) end;
	callfunc "MissionInfo";
	close;
}

prt_in,90,72,4	script	Zeph::HuntingSystem	732,{
	mes "[^FFA500Zeph^000000]";
	if( Mission_Tick < gettimetick(2) )
	{
		if( Mission_Tick == 0 )
		{
			mes "Hello...";
			mes "Are you interesting in a job?";
			next;
			mes "[^FFA500Zeph^000000]";
			mes "I've to explain, I find many peoples interesting in ^0000FFHunting Missions^000000 every day.";
			mes "The missions consist of monsters to kill.";
			next;
			mes "[^FFA500Zeph^000000]";
			mes "There are different difficulty Missions.";
			mes "But don't worry, you can work with your friends in groups or in any team.";
			next;
			mes "[^FFA500Zeph^000000]";
			mes "You will have exactly 24 hours to complete a mission, if you finish in time then come back to me for the premium.";
			
		}
		else if( Mission_Ended )
		{
			mes "Your new mission is ready, and I've looked over the documents.";
			mes "Are you ready to take on it?";
		}
		else
		{
			mes "You've failed your last mission, you didn't finish within 24 hours or doesn't eliminated the total requested monsters.";
			next;
			mes "[^FFA500Zeph^000000]";
			mes "There is your today's Mission.";
			mes "Are you ready to take on it?";
		}

		next;
		if( select("Yes, what is my job?:No thanks, not today.") == 2 )
		{
			mes "[^FFA500Zeph^000000]";
			mes "Okay, come back if you've changed your mind.";
			close;
		}

		mes "[^FFA500Zeph^000000]";
		mes "I let you reading...";
		next;
		
		// Building Mission...
		// =======================================================================================================
		mission_settime gettimetick(2) + 86400;
		// Mission Difficulty
		set .@Mission_Rank, ((Mission_Rank % 45) * 2) + 1;
		// Bonus Difficulty
		set .@Mission_Bonus, (Mission_Rank / 45) * 5;
		// Final Difficulty
		if( set(.@Mission_Rank, .@Mission_Rank + .@Mission_Bonus) > 99 ) set .@Mission_Rank, 99;
		set Mission_Ended, 0;
		for( set .@i, 1; .@i < 6; set .@i, .@i + 1 )
		{
			set .@MobID, getmobrandid(.@Mission_Rank, 14);
			// Mission Index - Mob ID - Mob Amount
			mission_sethunting .@i, .@MobID, 75 - (.@Mission_Rank / 2) + (.@Mission_Bonus / 2);
		}

		callfunc "MissionInfo";
		next;

		mes "[^FFA500Zeph^000000]";
		mes "Good luck, and remember that you have 24 hours to complete it.";
		mes "Come back if you're finished.";
		close;
	}
	else
	{
		mes "Hello, already here?";
		mes "Let's see how you completed the mission...";
		next;
		
		if( Mission_Count1 + Mission_Count2 + Mission_Count3 + Mission_Count4 + Mission_Count5 < 1 )
		{
			mes "[^FFA500Zeph^000000]";
			if( Mission_Ended == 0 )
			{
				mes "Great! You did it ahead of schedule.";
				mes "Let me pay you for the job.";
				next;

				set .@Mission_Lvl, 0;
				set .@Mission_Exp, 0;
				set .@Mission_Job, 0;
				for( set .@i, 1; .@i < 6; set .@i, .@i + 1 )
				{
					set .@MobID, getd("Mission_ID" + .@i);
					set .@Mission_Lvl, .@Mission_Lvl + strmobinfo(3, .@MobID);
					set .@Mission_Exp, .@Mission_Exp + (strmobinfo(6, .@MobID) * 10);
					set .@Mission_Job, .@Mission_Job + (strmobinfo(7, .@MobID) * 10);
				}
				set .@Mission_Zeny, .@Mission_Lvl * 150;
				if( .@Mission_Lvl < 125 )
					set .@Mission_Points, 5;
				else if( .@Mission_Lvl < 250 )
					set .@Mission_Points, 10;
				else
					set .@Mission_Points, 15;
				
				// Mission Rewards...
				// =====================================================================
				getexp .@Mission_Exp, .@Mission_Job, 100;
				set Zeny, Zeny + .@Mission_Zeny;
				set Mission_Points, Mission_Points + .@Mission_Points;
				if( Mission_Rank < 449 ) set Mission_Rank, Mission_Rank + 1;
				set Mission_Completed, Mission_Completed + 1;
				if( Mission_Completed > $TMission_Score )
				{
					npctalk "Congratulations, you're the new Top Hunter of the day!!";
					set $TMission_Score, Mission_Completed;
					set $TMission_CharID, getcharid(0);
					set $TMission_Name$, strcharinfo(0);
					set $TMission_Job$, jobname(Class);
				}

				if( $TMission_CharID == getcharid(0) )
					set #CASHPOINTS, #CASHPOINTS + 100;
				else
					set #CASHPOINTS, #CASHPOINTS + 75;

				getitem 8996,1;
				// =====================================================================
				
				// Clear Mission Data
				for( set .@i, 1; .@i < 6; set .@i, .@i + 1 )
					mission_sethunting .@i,0,0;
				set Mission_Ended, 1;
			}
			
			mes "[^FFA500Zeph^000000]";
			mes "Your today's Mission is now complete.";

			set .@Time_Left, Mission_Tick - gettimetick(2);
			if( .@Time_Left > 3600 )
				mes "You can get a new mission in ^0000FF" + (.@Time_Left / 3600) + " hours^000000.";
			else if( .@Time_Left > 60 )
				mes "You can get a new mission in ^0000FF" + (.@Time_Left / 60) + " minutes^000000.";
			else if( .@Time_Left > 0 )
				mes "You can get a new mission in ^0000FF" + (.@Time_Left) + " seconds^000000.";
			else
				mes "Let me sort out my papers to your new assignment.";
			close;
		}

		mes "[^FFA500Zeph^000000]";
		mes "You're still not completed the mission, your missing monsters will be removed.";
		mes "Look at the poster on my left to see the status of your job.";
		close;
	}
}

prt_in,86,72,4	script	Top Hunter	857,{
	mes "[^FFA500Best Hunter^000000]";
	if( $TMission_CharID )
	{
		mes "Name : ^0000FF" + $TMission_Name$ + "^000000";
		mes "Job : ^0000FF" + $TMission_Job$ + "^000000";
		mes "Score : ^0000FF" + $TMission_Score + "^000000";
	}
	else
	{
		mes "Name : ^0000FF(none)^000000";
		mes "Job : ^0000FF(none)^000000";
		mes "Score : ^0000FF0^000000";
	}

	next;
	mes "[^FFA500Best Hunter^000000]";
	mes "The first position is assigned to the best hunter who has completed the most missions.";
	mes "His prize is a bonus of 25 Cash Points while holding the first place.";
	close;
}

prt_in,85,61,6	script	Sophie::HuntingRewards	894,{
	mes "[^FFA500Sophie^000000]";
	mes "Hey, I'm Zeph's wife, Sophie.";
	mes "You can change your points and medals to items.";
	next;
	mes "[^FFA500Sophie^000000]";
	mes "Your stats:";
	mes "- Mission Completed: ^0000FF" + Mission_Completed + "^000000";
	mes "- Mission Points: ^0000FF" + Mission_Points + "^000000";
	mes "- Mission Medals: ^0000FF" + countitem(8996) + "^000000";
	next;
	switch( select("Invest Points:Invest Medals") )
	{
		case 1:
			mes "[^FFA500Sophie^000000]";
			mes "There is a list what you can get by investing ^0000FF15 Mission Points^000000. Please choose your option:";
			next;
			setarray .@Shop[0],504,150,505,75,7139,75,7138,200,7137,200,7135,50,7136,50,678,10;
			set .@Index, (select("75 White Potion:75 Blue Potion:75 Glistening Coat:200 Marine Sphere Bottle:200 Plant Bottle:50 Bottle Grenade:50 Acid Bottle:10 Poison Bottle") - 1) * 2;

			set .@Item, .@Shop[.@Index];
			set .@Amount, .@Shop[.@Index + 1];

			if( Mission_Points < 15 )
			{
				mes "[^FFA500Sophie^000000]";
				mes "You don't have enough Mission Point.";
				close;
			}

			if( checkweight(.@Item, .@Amount) == 0 )
			{
				mes "[^FFA500Sophie^000000]";
				mes "You don't have enough weight capacity.";
				close;
			}

			set Mission_Points, Mission_Points - 15;
			getitem .@Item, .@Amount;
			mes "[^FFA500Sophie^000000]";
			mes "Enjoy, and continue to doing more missions.";
			close;

		case 2:
			mes "[^FFA500Sophie^000000]";
			mes "These items can only be got by Mission Medals, say what you want to get.";
			next;
			setarray .@Shop[0],7776,10,2345,90,2347,90,2349,90,2351,90;
			set .@Index, (select("1 Gym Pass ^0000FF(10 Medals)^000000:1 Armor of Volcano[1] ^0000FF(90 Medals)^000000:1 Armor of Ocean[1] ^0000FF(90 Medals)^000000:1 Raging Typhoon Armor[1] ^0000FF(90 Medals)^000000:1 Cracking Earth Armor[1] ^0000FF(90 Medals)^000000") - 1) * 2;

			set .@Item, .@Shop[.@Index];
			set .@Amount, .@Shop[.@Index + 1];

			if( countitem(8996) < .@Amount )
			{
				mes "[^FFA500Sophie^000000]";
				mes "You don't have enough Mission Medal.";
				close;
			}

			if( checkweight(.@Item, 1) == 0 )
			{
				mes "[^FFA500Sophie^000000]";
				mes "You don't have enough weight capacity.";
				close;
			}

			delitem 8996, .@Amount;
			getitem .@Item, 1;
			mes "[^FFA500Sophie^000000]";
			mes "Enjoy, and continue to doing more missions.";
			close;
	}
}
